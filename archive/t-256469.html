<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" Multi-line DetailPrint, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  Multi-line DetailPrint NSIS Discussion" />
	
	<title> Multi-line DetailPrint [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  Multi-line DetailPrint</div>
<hr />
<div class="pda"><a href="t-256469.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=256469">Multi-line DetailPrint</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">zorphnog</div><div class="date">29th September 2006, 15:13</div></div><div class="posttext">Is there a way to correctly print strings with \r, \n, or \t when using the DetailPrint function? I could not find anything that will print the messages correctly. I'm in the process of writing a function that will do so, but I thought I would ask before I get too far into it. Thanks.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">kichik</div><div class="date">29th September 2006, 15:17</div></div><div class="posttext">You must use multiple DetailPrint commands. You need a function that'd split the line and call DetailPrint multiple times. You could probably use WordFind (http://nsis.sourceforge.net/Docs/AppendixE.html#E.3.2) to split the lines.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">zorphnog</div><div class="date">29th September 2006, 15:40</div></div><div class="posttext">Well I went ahead and made a function MultiDetailPrint. Here is the code for anyone who is interested. :D<br />
<br />
Name &quot;Multi-line DetailPrint Test&quot;<br />
<br />
OutFile &quot;MultiTest.exe&quot;<br />
<br />
ShowInstDetails show<br />
<br />
Section<br />
  StrCpy $0 &quot;This is$\r$\na multiple$\n$\tline string$\r$\n$\t$\twith tabs&quot;<br />
  Push $0<br />
  Call MultiDetailPrint<br />
SectionEnd<br />
<br />
; Function:    MultiDetailPrint<br />
; Written by:  M. Mims<br />
; Date:        9.29.2006<br />
; Description: Parses strings with return, newline, and tab characters to print<br />
;              correctly to the details screen.<br />
Function MultiDetailPrint<br />
   Exch $R0<br />
   Push $R1<br />
   Push $R2<br />
   Push $R3<br />
   Push $R4<br />
   StrLen $R1 $R0                      ; Get length of string<br />
   StrCpy $R2 -1                       ; Set character index<br />
<br />
 loop:<br />
   IntOp $R2 $R2 + 1                   ; Increase character index<br />
   StrCmp $R1 $R2 finish_trim          ; Finish if at end of string<br />
   StrCpy $R3 $R0 1 $R2                ; Get character at given index<br />
   StrCmp $R3 &quot;$\r&quot; r_trim_needed<br />
   StrCmp $R3 &quot;$\t&quot; t_trim_needed<br />
   StrCmp $R3 &quot;$\n&quot; n_trim_needed loop<br />
<br />
 r_trim_needed:<br />
   StrCpy $R3 $R0 $R2                  ; Copy left side of string<br />
   IntOp $R4 $R2 + 1                   ; Get index of next character<br />
   StrCpy $R0 $R0 $R1 $R4              ; Copy right side of string<br />
   StrCpy $R0 &quot;$R3$R0&quot;                 ; Merge string without \r<br />
   IntOp $R1 $R1 - 1                   ; Decrease string length<br />
   IntOp $R2 $R2 - 1                   ; Decrease index<br />
   goto loop<br />
<br />
 t_trim_needed:<br />
   StrCpy $R3 $R0 $R2                  ; Copy left side<br />
   IntOp $R4 $R2 + 1                   ; Index of next character<br />
   StrCpy $R0 $R0 $R1 $R4              ; Copy right side<br />
   StrCpy $R0 &quot;$R3        $R0&quot;         ; Merge with spaces<br />
   IntOp $R1 $R1 + 7                   ; Increase string length<br />
   IntOp $R2 $R2 + 7                   ; Increase index<br />
   goto loop<br />
<br />
 n_trim_needed:<br />
   StrCpy $R3 $R0 $R2                  ; Copy left side<br />
   IntOp $R4 $R2 + 1                   ; Index of next character<br />
   StrCpy $R0 $R0 $R1 $R4              ; Copy right side<br />
   DetailPrint $R3                     ; Print line<br />
   IntOp $R1 $R1 - $R2                 ; Adjust string length<br />
   IntOp $R1 $R1 - 1<br />
   StrCpy $R2 -1                       ; Adjust index<br />
   goto loop<br />
<br />
 finish_trim:<br />
   DetailPrint $R0                     ; Print final line<br />
   Pop $R4<br />
   Pop $R3<br />
   Pop $R2<br />
   Pop $R1<br />
   Exch $R0<br />
FunctionEnd</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>