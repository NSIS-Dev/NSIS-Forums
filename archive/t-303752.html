<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" backgroundcolor in every Dialog, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  backgroundcolor in every Dialog NSIS Discussion" />
	
	<title> backgroundcolor in every Dialog [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  backgroundcolor in every Dialog</div>
<hr />
<div class="pda"><a href="t-303752.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=303752">backgroundcolor in every Dialog</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">Fred4711</div><div class="date">4th March 2009, 10:05</div></div><div class="posttext">hi,<br />
sorry for the newbie question, but i want to set the backgroundcolor in every Dialog. and i cant do that.<br />
I read the manuel and try this one<br />
<br />
FindWindow $0 &quot;#32770&quot; &quot;&quot; $HWNDPARENT<br />
GetDlgItem $0 $0 1006<br />
SetCtlColors $0 0xFF0000 0x00FF00<br />
<br />
in the oninit Sequence. But the Licence Page still the normal Windows Dialog Colors. I dont know howto use this.<br />
Can Someone Help ? Or give me some example ?<br />
Greets<br />
Fred</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">4th March 2009, 10:23</div></div><div class="posttext">For any of the dialogs/controls that don't conform, adjust the colors separately.  Note that, depending on the control, this could be quite a few :)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Fred4711</div><div class="date">4th March 2009, 10:39</div></div><div class="posttext">Originally posted by Animaether <br />
For any of the dialogs/controls that don't conform, adjust the colors separately.  Note that, depending on the control, this could be quite a few :)  <br />
<br />
So know i can change the Color from the Header with <br />
!define MUI_BGCOLOR &quot;FF0000&quot;<br />
in the Example to red. Thats great ( for me ! )<br />
But know i down know how to set the Color of the Text from the header :-(<br />
And i alsaw doesnt know how i can change the Body Background Color. At this Time i try to change the Color from the Licence Page.<br />
I realy dont know how :-(</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">4th March 2009, 12:31</div></div><div class="posttext">You mean for the EULA text itself?<br />
<br />
If so - I'm afraid you may be SOL there (would have to review some plugins or so).  The richedit control used doesn't respect background color codes in the RTF file you'd feed it (the colors come from the RTF, so you can't set it with SetCtlColors).<br />
<br />
If you mean any of the other bits...<br />
<br />
!include &quot;winmessages.nsh&quot;<br />
!include &quot;MUI2.NSH&quot;<br />
<br />
BrandingText &quot; &quot;<br />
<br />
!define MUI_LICENSEPAGE_TEXT_TOP &quot;EULA Top Text&quot;<br />
!define MUI_LICENSEPAGE_TEXT_BOTTOM &quot;EULA Bottom Text&quot;<br />
; !define MUI_LICENSEPAGE_CHECKBOX<br />
; !define MUI_LICENSEPAGE_CHECKBOX_TEXT &quot;EULA Checkbox&quot;<br />
<br />
!define MUI_LICENSEPAGE_RADIOBUTTONS<br />
!define MUI_LICENSEPAGE_RADIOBUTTONS_TEXT_ACCEPT &quot;EULA Accept&quot;<br />
!define MUI_LICENSEPAGE_RADIOBUTTONS_TEXT_DECLINE &quot;EULA Decline&quot;<br />
<br />
!define MUI_PAGE_CUSTOMFUNCTION_PRE LicensePre<br />
!define MUI_PAGE_CUSTOMFUNCTION_SHOW LicenseShow<br />
!insertmacro MUI_PAGE_LICENSE &quot;eula.rtf&quot;<br />
; Page License LicensePre LicenseShow<br />
<br />
Function LicensePre<br />
	!insertmacro MUI_HEADER_TEXT &quot;This is the title&quot; &quot;This is the subtitle&quot;<br />
FunctionEnd<br />
<br />
Function LicenseShow<br />
	SetCtlColors $HWNDPARENT 0x220000 0x000000  ; Main dialog<br />
<br />
	FindWindow $1 &quot;#32770&quot; &quot;&quot; $HWNDPARENT       ; Inner dialog<br />
	SetCtlColors $1 0x440000 0x220000<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x40A             ; MUI2 header background<br />
	SetCtlColors $0 0x660000 0x440000<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x40D             ; MUI2 header title<br />
	SetCtlColors $0 0x880000 0x660000<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x40E             ; MUI2 header subtitle<br />
	SetCtlColors $0 0xaa0000 0x880000<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x4E8             ; Branding Text<br />
	SendMessage $0 ${WM_SETTEXT} 0 &quot;STR:Hello World&quot;<br />
	SetCtlColors $0 0xcc0000 0xaa0000<br />
<br />
	GetDlgItem $0 $1 0x410             ; Text above eula<br />
	SetCtlColors $0 0x008800 0x006600<br />
<br />
	GetDlgItem $0 $1 0x3EE             ; Text below eula<br />
	SetCtlColors $0 0x00aa00 0x008800<br />
<br />
	GetDlgItem $0 $1 0x40A             ; Eula checkbox / Accept radiobutton<br />
	SetCtlColors $0 0x00cc00 0x00aa00<br />
<br />
	GetDlgItem $0 $1 0x40B             ; Eula Decline radiobutton<br />
	SetCtlColors $0 0x00ee00 0x00cc00<br />
<br />
	GetDlgItem $0 $1 0x3E8             ; Eula body; can't change colors, they're defined by the Rich Text content.<br />
										; Note that the control used ignores background colors specified in the RTF - sorry :\<br />
										<br />
	GetDlgItem $0 $HWNDPARENT 0x1   ; Close/Next Button<br />
	EnableWindow $0 1<br />
	SetCtlColors $0 0x0000aa 0x000088<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x2   ; Cancel Button<br />
	EnableWindow $0 1<br />
	SetCtlColors $0 0x0000cc 0x0000aa<br />
<br />
	GetDlgItem $0 $HWNDPARENT 0x3   ; Back Button<br />
	SendMessage $0 ${WM_SETTEXT} 0 &quot;STR: Back&quot;<br />
	ShowWindow $0 ${SW_SHOW}<br />
	EnableWindow $0 1<br />
	SetCtlColors $0 0x0000ee 0x0000cc<br />
FunctionEnd<br />
<br />
Outfile &quot;test.exe&quot;<br />
Section<br />
SectionEnd<br />
!insertmacro MUI_LANGUAGE &quot;English&quot;<br />
<br />
<br />
Note that with most windows styles, the button faces will mostly ignore the colors set (only the 'shaded' edges will get the colors).</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">4th March 2009, 13:04</div></div><div class="posttext">presumably this might help...<br />
http://msdn.microsoft.com/en-us/library/bb774228(VS.85).aspx<br />
...but I can't get it to work.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Fred4711</div><div class="date">4th March 2009, 13:05</div></div><div class="posttext">hi thanks !<br />
I try it ...</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>