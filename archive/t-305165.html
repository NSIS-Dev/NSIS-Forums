<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" NSD_SetText Crash in Win2k, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  NSD_SetText Crash in Win2k NSIS Discussion" />
	
	<title> NSD_SetText Crash in Win2k [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  NSD_SetText Crash in Win2k</div>
<hr />
<div class="pda"><a href="t-305165.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=305165">NSD_SetText Crash in Win2k</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">Spilly</div><div class="date">9th April 2009, 19:12</div></div><div class="posttext">The following code has no issues in xp or vista, but crashes in 2k when NSD_SetText is called. <br />
<br />
${NSD_CreateText} $(NSD_X) $(NSD_Y) $(NSD_WIDTH) $(NSD_HEIGHT) &quot;$(NSD_TEXT)&quot;<br />
Pop $0<br />
${NSD_OnChange} $0 clearTextBoxWhenClicked<br />
...<br />
<br />
Function clearTextBoxWhenClicked            <br />
    Exch $0<br />
    ${NSD_SetText} $0 &quot;&quot;<br />
    Pop $0<br />
FunctionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">demiller9</div><div class="date">9th April 2009, 19:35</div></div><div class="posttext">The EXCH $0 in clearTextBoxWhenClicked is going to pop a value from the stack, but your code snippet doesn't show that anything is on the stack. Is that why it crashes?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">9th April 2009, 19:39</div></div><div class="posttext">What happens when you manually use...<br />
<br />
SendMessage $0 ${WM_SETTEXT} 0 `STR:`<br />
<br />
<br />
What happens if you use a non-empty string? (i.e. instead of &quot;&quot; use &quot; &quot;)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Spilly</div><div class="date">10th April 2009, 01:05</div></div><div class="posttext">demiller9, NSD_OnChange puts the control's HWND on the stack when the callback is called. <br />
<br />
Animaether, I tried both suggestions and each crashed the 2k machine. If I comment out the NSD_SetText call, there's no crash at all.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Anders</div><div class="date">10th April 2009, 03:32</div></div><div class="posttext">I'm sorry but, how is this not a loop that will never end?<br />
<br />
Are you trying to create one of those &quot;Search...&quot; things that go away when you click the edit box?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">10th April 2009, 04:41</div></div><div class="posttext">... NSD_SetText triggers the OnChange function? brr.  Well that might indeed explain it (although not explain why he'd only see it on 2K).<br />
<br />
I suppose the solution is to only change the text when it needs changing;<br />
<br />
!addplugindir &quot;.&quot;<br />
!addincludedir &quot;.&quot;<br />
<br />
!include &quot;nsDialogs.nsh&quot;<br />
!include &quot;winmessages.nsh&quot;<br />
!include &quot;logiclib.nsh&quot;<br />
<br />
!include &quot;MUI2.nsh&quot;<br />
<br />
OutFile &quot;test.exe&quot;<br />
<br />
Var dialog<br />
Var hwnd<br />
Var null<br />
<br />
Page Custom CustomPage<br />
<br />
Function CustomPage<br />
	nsDialogs::Create 1018<br />
	Pop $dialog<br />
<br />
	${NSD_CreateText} 0 0 100% 8% &quot;Hello world&quot;<br />
	    Pop $hwnd<br />
	    ${NSD_OnChange} $hwnd TextChanged<br />
	    ${NSD_OnClick} $hwnd TextClicked<br />
	nsDialogs::Show<br />
FunctionEnd<br />
<br />
Function TextChanged<br />
	Pop $hwnd<br />
	MessageBox MB_OK &quot;Text Changed&quot;<br />
	${NSD_GetText} $hwnd $0<br />
	${Unless} $0 == &quot;Foo&quot;<br />
		${NSD_SetText} $hwnd &quot;Foo&quot;<br />
	${EndUnless}<br />
FunctionEnd<br />
<br />
Section<br />
SectionEnd<br />
<br />
!insertmacro MUI_LANGUAGE &quot;English&quot;<br />
<br />
<br />
If you are indeed trying to create what Anders mentioned, you might have to track focus (as there's no OnFocus type callback yet);<br />
<br />
<br />
!addplugindir &quot;.&quot;<br />
!addincludedir &quot;.&quot;<br />
<br />
!include &quot;nsDialogs.nsh&quot;<br />
!include &quot;winmessages.nsh&quot;<br />
!include &quot;logiclib.nsh&quot;<br />
<br />
!include &quot;MUI2.nsh&quot;<br />
<br />
OutFile &quot;test.exe&quot;<br />
<br />
Var dialog<br />
Var hwnd<br />
Var null<br />
<br />
var NSD_TextField<br />
var NSD_Focus_hwnd<br />
<br />
Page Custom CustomPage<br />
<br />
Function CustomPage<br />
	nsDialogs::Create 1018<br />
	Pop $dialog<br />
<br />
	${NSD_CreateText} 0 0 100% 8% &quot;Password&quot;<br />
	    Pop $NSD_TextField<br />
	    SetCtlColors $NSD_TextField 0x888888 0xffffff<br />
	    ${NSD_OnChange} $NSD_TextField TextChanged<br />
	    <br />
	${NSD_CreateTimer} FocusTimer 100<br />
<br />
	nsDialogs::Show<br />
FunctionEnd<br />
<br />
Function TextChanged<br />
	Pop $hwnd<br />
	; text change code here<br />
FunctionEnd<br />
<br />
Function FocusTimer<br />
	System::Call &quot;user32::GetFocus(v) i.r0&quot; ; get element with current focus in $0<br />
	${If} $0 != $NSD_Focus_hwnd<br />
	    StrCpy $NSD_Focus_hwnd $0<br />
	    ${If} $NSD_Focus_hwnd == $NSD_TextField<br />
	        ${NSD_KillTimer} FocusTimer<br />
		    ${NSD_GetText} $NSD_TextField $0<br />
	        ${If} $0 == &quot;Password&quot;<br />
		        ${NSD_SetText} $NSD_TextField &quot;&quot;<br />
			    SetCtlColors $NSD_TextField 0x000000 0xffffff<br />
			${EndIf}<br />
		${EndIf}<br />
	${EndIf}<br />
FunctionEnd<br />
<br />
Section<br />
SectionEnd<br />
<br />
!insertmacro MUI_LANGUAGE &quot;English&quot;</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Anders</div><div class="date">10th April 2009, 06:03</div></div><div class="posttext">actually, the edit box on XP and later has built-in support for showing those &quot;help texts&quot;</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">10th April 2009, 06:58</div></div><div class="posttext">Originally posted by Anders <br />
actually, the edit box on XP and later has built-in support for showing those &quot;help texts&quot;  <br />
<br />
Unfortunately it seems he requires 2K support.<br />
<br />
But if you could give an example of EM_SETCUEBANNER working with the NSD_CreateText (I'm failing at it), I'd be happy to add it to the nsDialogs snippets.<br />
( not sure if NSIS ends up using common controls v6, already used the System plugin to explicitly specify a unicode string)<br />
<br />
Though browsing around, I see that..<br />
- it won't work on multi-line controls<br />
- it won't work on any XP install that has asian language support installed<br />
<br />
The above solution - though not pretty due to the use of a timer - should work regardless and give you more control.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Anders</div><div class="date">10th April 2009, 07:44</div></div><div class="posttext">NSD should not have a timer in the first place IMHO, it does not belong there at all, all you can do with it are hacks. There is a subclass plugin, its not pretty, but its the right tool for this job</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">10th April 2009, 08:20</div></div><div class="posttext">Given three options:<br />
- you can't do this<br />
- you can do this, good luck figuring it out*<br />
- use hacks<br />
<br />
Many people would use hacks if the appropriate means are too cumbersome.  If you're stuck on a roof mounting a satellite dish with a wrench and you happen to note a roof tile nail is loose, but your hammer is 2 flights down, you betcha you'll just tap that nail back in with the wrench.. no matter how inappropriate a wrench is for serious hammering purposes.<br />
<br />
Granted, I don't use the timer very often, but even just as a post-show function (i.e. the nsDialogs::show command has been run, all code after that halts.. which means you're stuck if you want to update something on that dialog without further user interaction) it does the job just fine as a 'hack'.<br />
<br />
<br />
*That said... the 'not pretty' plugin...<br />
http://nsis.sourceforge.net/WndSubclass_plug-in<br />
...does the job just fine; although I might not be using it correctly as its current state appears to be alpha (from another thread).<br />
<br />
<br />
!addplugindir &quot;.&quot;<br />
!addincludedir &quot;.&quot;<br />
<br />
!include &quot;nsDialogs.nsh&quot;<br />
!include &quot;winmessages.nsh&quot;<br />
!include &quot;logiclib.nsh&quot;<br />
<br />
!include &quot;MUI2.nsh&quot;<br />
<br />
!include &quot;WndSubclass.nsh&quot;<br />
<br />
OutFile &quot;test.exe&quot;<br />
<br />
var dialog<br />
var hwnd<br />
var null<br />
<br />
Page Custom CustomPage<br />
<br />
var NSD_TextField<br />
var NSD_Label<br />
var TextFieldSubProc<br />
<br />
Function CustomPage<br />
	nsDialogs::Create 1018<br />
	Pop $dialog<br />
<br />
	${NSD_CreateText} 0 0 100% 8% &quot;Password&quot;<br />
	    Pop $NSD_TextField<br />
		SetCtlColors $NSD_TextField 0x888888 0xffffff<br />
	    ${WndSubclass_Subclass} $NSD_TextField TextFieldSubProc $TextFieldSubProc $TextFieldSubProc<br />
<br />
	${NSD_CreateLabel} 0 10% 100% 8% &quot;&quot;<br />
	    Pop $NSD_Label<br />
<br />
	nsDialogs::Show<br />
FunctionEnd<br />
<br />
Function TextFieldSubProc<br />
	${If} $2 = ${WM_SETFOCUS}<br />
		${NSD_GetText} $NSD_TextField $R0<br />
		${If} $R0 == &quot;Password&quot;<br />
			${NSD_SetText} $NSD_TextField &quot;&quot;<br />
			SetCtlColors $NSD_TextField 0x000000 0xffffff<br />
		${EndIf}<br />
	${ElseIf} $2 = ${WM_KILLFOCUS}<br />
		${NSD_GetText} $NSD_TextField $R0<br />
		${If} $R0 == &quot;&quot;<br />
			${NSD_SetText} $NSD_TextField &quot;Password&quot;<br />
			SetCtlColors $NSD_TextField 0x888888 0xffffff<br />
		${EndIf}<br />
	${EndIf}<br />
FunctionEnd<br />
<br />
Function TextChanged<br />
	Pop $hwnd<br />
	; text change code here<br />
FunctionEnd<br />
<br />
Section<br />
SectionEnd<br />
<br />
!insertmacro MUI_LANGUAGE &quot;English&quot;</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Anders</div><div class="date">10th April 2009, 08:46</div></div><div class="posttext">and it will ALWAYS be alpha, I'm google--</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">10th April 2009, 08:49</div></div><div class="posttext">*bows to Anders*<br />
<br />
Anyway.. uhh.. Spilly - I hope one of these resolves your problem :)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Spilly</div><div class="date">16th April 2009, 18:18</div></div><div class="posttext">Wow, thanks for the help guys. It seemed a little too much code to add simply for win2k support, so I was going to remove this feature and add the text above the text box instead, but I found a much more simple solution. <br />
<br />
Here it is:<br />
<br />
Var SetTextOnce<br />
${NSD_CreateText} $(NSD_X) $(NSD_Y) $(NSD_WIDTH) $(NSD_HEIGHT) &quot;$(NSD_TEXT)&quot;<br />
Pop $0<br />
StrCpy $SetTextOnce 0<br />
${NSD_OnChange} $0 clearTextBoxWhenClicked<br />
...<br />
<br />
Function clearTextBoxWhenClicked <br />
Exch $0<br />
StrCmp $SetTextOnce 0 0 +3<br />
${NSD_SetText} $0 &quot;&quot;<br />
StrCpy $SetTextOnce 1<br />
Pop $0<br />
FunctionEnd</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>