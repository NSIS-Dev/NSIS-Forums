<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" in .OnInit StrCpy $INSTDIR &quot;c:\MYDIR&quot; has no effect?, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  in .OnInit StrCpy $INSTDIR &quot;c:\MYDIR&quot; has no effect? NSIS Discussion" />
	
	<title> in .OnInit StrCpy $INSTDIR &quot;c:\MYDIR&quot; has no effect? [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  in .OnInit StrCpy $INSTDIR &quot;c:\MYDIR&quot; has no effect?</div>
<hr />
<div class="pda"><a href="t-326317.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=326317">in .OnInit StrCpy $INSTDIR &quot;c:\MYDIR&quot; has no effect?</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">bdbaddog</div><div class="date">22nd January 2011, 01:29</div></div><div class="posttext">Greetings,<br />
<br />
Here's my complete .OnInit<br />
<br />
Function .onInit<br />
  # Change default base dir to C:\COMPANY<br />
  StrCpy $INSTDIR &quot;C:\COMPANY&quot;<br />
<br />
  InitPluginsDir<br />
  !insertmacro MULTIUSER_INIT<br />
<br />
  ReadRegStr $R0 HKLM \<br />
  &quot;Software\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; \<br />
  &quot;UninstallString&quot;<br />
  StrCmp $R0 &quot;&quot; done<br />
 <br />
  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \<br />
  &quot;$(^Name) is already installed. $\n$\nClick `OK` to remove the \<br />
  previous version or `Cancel` to cancel this upgrade.&quot; \<br />
  IDOK uninst<br />
  Abort<br />
 <br />
;Run the uninstaller<br />
uninst:<br />
  ClearErrors<br />
<br />
  # let's get the install dir of previous version by copying all <br />
  # but the /uinstall.exe from the full path to the uninstaller.exe<br />
  #  <br />
  StrCpy $R1 $R0 -13<br />
  ExecWait '$R0 _?=$R1' ;Do not copy the uninstaller to a temp file<br />
   <br />
  IfErrors no_remove_uninstaller done<br />
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code<br />
    ;here to remove the uninstaller. Use a registry key to check<br />
    ;whether the user has chosen to uninstall. If you are using an uninstaller<br />
    ;components page, make sure all sections are uninstalled.<br />
  no_remove_uninstaller:<br />
 <br />
done:<br />
<br />
FunctionEnd<br />
<br />
When I run the installer it still prompts me to install into:<br />
c:\Program Files (x86)\myProduct<br />
<br />
It seems like StrCpy  $INSTDIR in the .OnInit is having no effect.<br />
<br />
Any sugguestions on how to make this work?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">jiake</div><div class="date">22nd January 2011, 07:12</div></div><div class="posttext">For that you have specified InstallDir in your script, to replace the defualt one, using InstallDir $INSTDIR.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">bdbaddog</div><div class="date">23rd January 2011, 02:43</div></div><div class="posttext">Thanks for the suggestion.<br />
It wasn't in my file.<br />
I changed it, an still no change.<br />
<br />
When it gets to the &quot;Chose Install Location&quot; page, it still defaulting to<br />
c:\Program Files (x86)\Blah<br />
<br />
I created a dummy installer with .onInit setting $INSTDIR and InstallDir $INSTDIR see below. It also doesn't obey my StrCpy $INSTDIR &quot;C:\BLAH&quot;..<br />
<br />
Can someone else try the below, you'll need a README.txt in your work dir..<br />
<br />
I'm hoping it's something really simple that I&quot;m missing.<br />
<br />
_Bill<br />
<br />
# Auto-generated by EclipseNSIS Script Wizard<br />
# Jan 22, 2011 6:37:58 PM<br />
<br />
Name &quot;My Application&quot;<br />
<br />
# General Symbol Definitions<br />
!define REGKEY &quot;SOFTWARE\$(^Name)&quot;<br />
<br />
# MultiUser Symbol Definitions<br />
!define MULTIUSER_EXECUTIONLEVEL Standard<br />
!define MULTIUSER_INSTALLMODE_COMMANDLINE<br />
!define MULTIUSER_INSTALLMODE_INSTDIR &quot;My Application&quot;<br />
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY &quot;${REGKEY}&quot;<br />
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUE &quot;Path&quot;<br />
<br />
# MUI Symbol Definitions<br />
!define MUI_FINISHPAGE_NOAUTOCLOSE<br />
!define MUI_STARTMENUPAGE_REGISTRY_ROOT HKLM<br />
!define MUI_STARTMENUPAGE_NODISABLE<br />
!define MUI_STARTMENUPAGE_REGISTRY_KEY ${REGKEY}<br />
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME StartMenuGroup<br />
!define MUI_STARTMENUPAGE_DEFAULTFOLDER &quot;My Application&quot;<br />
!define MUI_UNFINISHPAGE_NOAUTOCLOSE<br />
<br />
# Included files<br />
!include MultiUser.nsh<br />
!include Sections.nsh<br />
!include MUI2.nsh<br />
<br />
# Variables<br />
Var StartMenuGroup<br />
<br />
# Installer pages<br />
!insertmacro MUI_PAGE_WELCOME<br />
!insertmacro MUI_PAGE_DIRECTORY<br />
!insertmacro MUI_PAGE_STARTMENU Application $StartMenuGroup<br />
!insertmacro MUI_PAGE_INSTFILES<br />
!insertmacro MUI_PAGE_FINISH<br />
!insertmacro MUI_UNPAGE_CONFIRM<br />
!insertmacro MUI_UNPAGE_INSTFILES<br />
<br />
# Installer languages<br />
!insertmacro MUI_LANGUAGE English<br />
<br />
# Installer attributes<br />
OutFile setup.exe<br />
InstallDir $INSTDIR<br />
CRCCheck on<br />
XPStyle on<br />
ShowInstDetails hide<br />
InstallDirRegKey HKLM &quot;${REGKEY}&quot; Path<br />
ShowUninstDetails hide<br />
<br />
# Installer sections<br />
Section -Main SEC0000<br />
    SetOutPath $INSTDIR<br />
    SetOverwrite on<br />
    File README.txt<br />
    WriteRegStr HKLM &quot;${REGKEY}\Components&quot; Main 1<br />
SectionEnd<br />
<br />
Section -post SEC0001<br />
    WriteRegStr HKLM &quot;${REGKEY}&quot; Path $INSTDIR<br />
    SetOutPath $INSTDIR<br />
    WriteUninstaller $INSTDIR\uninstall.exe<br />
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application<br />
    SetOutPath $SMPROGRAMS\$StartMenuGroup<br />
    CreateShortcut &quot;$SMPROGRAMS\$StartMenuGroup\Uninstall $(^Name).lnk&quot; $INSTDIR\uninstall.exe<br />
    !insertmacro MUI_STARTMENU_WRITE_END<br />
    WriteRegStr HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; DisplayName &quot;$(^Name)&quot;<br />
    WriteRegStr HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; DisplayIcon $INSTDIR\uninstall.exe<br />
    WriteRegStr HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; UninstallString $INSTDIR\uninstall.exe<br />
    WriteRegDWORD HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; NoModify 1<br />
    WriteRegDWORD HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot; NoRepair 1<br />
SectionEnd<br />
<br />
# Macro for selecting uninstaller sections<br />
!macro SELECT_UNSECTION SECTION_NAME UNSECTION_ID<br />
    Push $R0<br />
    ReadRegStr $R0 HKLM &quot;${REGKEY}\Components&quot; &quot;${SECTION_NAME}&quot;<br />
    StrCmp $R0 1 0 next${UNSECTION_ID}<br />
    !insertmacro SelectSection &quot;${UNSECTION_ID}&quot;<br />
    GoTo done${UNSECTION_ID}<br />
next${UNSECTION_ID}:<br />
    !insertmacro UnselectSection &quot;${UNSECTION_ID}&quot;<br />
done${UNSECTION_ID}:<br />
    Pop $R0<br />
!macroend<br />
<br />
# Uninstaller sections<br />
Section /o -un.Main UNSEC0000<br />
    Delete /REBOOTOK $INSTDIR\README.txt<br />
    DeleteRegValue HKLM &quot;${REGKEY}\Components&quot; Main<br />
SectionEnd<br />
<br />
Section -un.post UNSEC0001<br />
    DeleteRegKey HKLM &quot;SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$(^Name)&quot;<br />
    Delete /REBOOTOK &quot;$SMPROGRAMS\$StartMenuGroup\Uninstall $(^Name).lnk&quot;<br />
    Delete /REBOOTOK $INSTDIR\uninstall.exe<br />
    DeleteRegValue HKLM &quot;${REGKEY}&quot; StartMenuGroup<br />
    DeleteRegValue HKLM &quot;${REGKEY}&quot; Path<br />
    DeleteRegKey /IfEmpty HKLM &quot;${REGKEY}\Components&quot;<br />
    DeleteRegKey /IfEmpty HKLM &quot;${REGKEY}&quot;<br />
    RmDir /REBOOTOK $SMPROGRAMS\$StartMenuGroup<br />
    RmDir /REBOOTOK $INSTDIR<br />
SectionEnd<br />
<br />
# Installer functions<br />
Function .onInit<br />
    StrCpy $INSTDIR &quot;C:\BLAHBLAH&quot;<br />
    InitPluginsDir<br />
    !insertmacro MULTIUSER_INIT<br />
FunctionEnd<br />
<br />
# Uninstaller functions<br />
Function un.onInit<br />
    !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuGroup<br />
    !insertmacro MULTIUSER_UNINIT<br />
    !insertmacro SELECT_UNSECTION Main ${UNSEC0000}<br />
FunctionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">MSG</div><div class="date">23rd January 2011, 09:01</div></div><div class="posttext">I think &quot;InstallDir $INSTDIR&quot; is kind of pointless. the idea is to set the value of $instdir...</div></div><hr />


<div class="post"><div class="posttop"><div class="username">bdbaddog</div><div class="date">25th January 2011, 19:01</div></div><div class="posttext">Either way.. The sample script doesn't alter the default install dir.<br />
<br />
Doe anyone have a sample script which does?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Afrow UK</div><div class="date">25th January 2011, 21:52</div></div><div class="posttext">Try commenting out the two !define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_* lines.<br />
<br />
Stu</div></div><hr />


<div class="post"><div class="posttop"><div class="username">bdbaddog</div><div class="date">25th January 2011, 23:06</div></div><div class="posttext">This works<br />
<br />
Function .onInit<br />
    InitPluginsDir<br />
    !insertmacro MULTIUSER_INIT<br />
    StrCpy $INSTDIR &quot;C:\BLAHBLAH&quot;<br />
FunctionEnd<br />
<br />
This doesn't work<br />
<br />
Function .onInit<br />
    StrCpy $INSTDIR &quot;C:\BLAHBLAH&quot;<br />
    InitPluginsDir<br />
    !insertmacro MULTIUSER_INIT<br />
FunctionEnd<br />
<br />
It looks like &quot;!insermacro MULTIUSER_INIT&quot; must overwrite $INSTDIR</div></div><hr />


<div class="post"><div class="posttop"><div class="username">bdbaddog</div><div class="date">26th January 2011, 00:26</div></div><div class="posttext">Also found you need to not define<br />
MULTIUSER_INSTALLMODE_INSTDIR<br />
<br />
Or it will prefix $INSTDIR as follows<br />
<br />
    !ifdef MULTIUSER_INSTALLMODE_INSTDIR<br />
      StrCpy $INSTDIR &quot;$PROGRAMFILES\${MULTIUSER_INSTALLMODE_INSTDIR}&quot;<br />
    !endif</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>