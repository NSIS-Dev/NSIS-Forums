<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" Check a control, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  Check a control NSIS Discussion" />
	
	<title> Check a control [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  Check a control</div>
<hr />
<div class="pda"><a href="t-304446.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=304446">Check a control</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">Lupo73</div><div class="date">21st March 2009, 20:45</div></div><div class="posttext">I'm using nsDialogs.. I have created a custom page where the user can load a file (with SelectFileDialog) and now I need to make a check, when the user click Next, to verify if the file selected is valid or not. If is not valid (if the name of the file doesn't contain the string &quot;Pack&quot;) I need to open a message that ask to the user to select a correct file. Thanks for help!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Red Wine</div><div class="date">21st March 2009, 23:16</div></div><div class="posttext">You will need a callback function.<br />
I guess you'll find all the info you need into nsDialogs documentation.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Lupo73</div><div class="date">22nd March 2009, 10:00</div></div><div class="posttext">But how can I set to don't leave the custom page if a variable has particular values?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">22nd March 2009, 14:10</div></div><div class="posttext">If you're using nsDialogs, then it won't leave the page anyway (that was a behavior of InstallOptions(2/Ex)).  If you want to enable/disable the 'Next' button, you can do so in code as well.<br />
<br />
The following code simply checks folder name validity - should be simple enough to adjust to check whether the specified file has 'pack' in it;<br />
<br />
!include &quot;winmessages.nsh&quot;<br />
!include &quot;LogicLib.nsh&quot;<br />
!include &quot;nsDialogs.nsh&quot;<br />
<br />
Outfile &quot;test.exe&quot;<br />
<br />
var dialog<br />
var hwnd<br />
var null<br />
<br />
!macro RedrawControl control<br />
	Push $0<br />
	StrCpy $0 ${control}<br />
	System::Call &quot;user32::InvalidateRect(i,i,i)i (r0, 0, 1)&quot;<br />
	Pop $0<br />
!macroend<br />
!define RedrawControl `!insertmacro RedrawControl`<br />
<br />
var InstallDir ; Use a variable separate from $INSTDIR to by-pass automatic adjustments<br />
<br />
Function .onInit<br />
	StrCpy $InstallDir &quot;$PROGRAMFILES\SomeFolder&quot;<br />
FunctionEnd<br />
<br />
<br />
Page custom page.settings<br />
Page InstFiles<br />
<br />
var NSD_InstallDir<br />
var NSD_InstallDirStatus<br />
Function page.settings<br />
	nsDialogs::Create 1018<br />
	Pop $dialog<br />
<br />
	${NSD_CreateText} 3% 3% 89% 8% &quot;$InstallDir&quot;<br />
		Pop $NSD_InstallDir<br />
		${NSD_OnChange} $NSD_InstallDir page.settings.installdir.onchange<br />
<br />
	${NSD_CreateButton} 92% 3% 5% 8% &quot;...&quot;<br />
		Pop $hwnd<br />
		${NSD_OnClick} $hwnd page.settings.installdir.onclick<br />
		<br />
	${NSD_CreateLabel} 3% 12% 100% 8% &quot;status: &quot;<br />
	    Pop $NSD_InstallDirStatus<br />
<br />
	; Initialization<br />
	Push $NSD_InstallDir<br />
	Call page.settings.installdir.onchange<br />
<br />
	nsDialogs::Show<br />
	Abort<br />
FunctionEnd<br />
<br />
Function page.settings.installdir.onchange<br />
	Pop $hwnd<br />
	${NSD_GetText} $hwnd $InstallDir<br />
<br />
	; Kill trailing backslash<br />
	StrCpy $0 $InstallDir 1 -1<br />
	${If} $0 == &quot;\&quot;<br />
	    StrLen $0 $InstallDir<br />
	    IntOp $0 $0 - 1<br />
	    StrCpy $InstallDir $InstallDir $0<br />
	${EndIf}<br />
<br />
	StrCpy $0 $InstallDir<br />
<br />
	; Kill invalid characters<br />
	Push $InstallDir<br />
	    Exch $EXEDIR<br />
	    Exch $EXEDIR<br />
	Pop $InstallDir<br />
<br />
	GetDlgItem $1 $HWNDPARENT 1 ; Next button<br />
<br />
    ${If} $InstallDir == &quot;&quot;<br />
		${NSD_SetText} $NSD_InstallDirStatus &quot;status: No destination folder specified&quot;<br />
		EnableWindow $1 0<br />
    ${ElseIf} $InstallDir != $0<br />
		${NSD_SetText} $NSD_InstallDirStatus &quot;status: Destination folder contains disallowed characters&quot;<br />
		EnableWindow $1 0<br />
	${ElseIf} ${FileExists} &quot;$InstallDir\*.*&quot;<br />
		${NSD_SetText} $NSD_InstallDirStatus &quot;status: OK - Destination folder exists&quot;<br />
		EnableWindow $1 1<br />
	${Else}<br />
		${NSD_SetText} $NSD_InstallDirStatus &quot;status: OK - Destination folder will be created&quot;<br />
		EnableWindow $1 1<br />
	${EndIf}<br />
FunctionEnd<br />
<br />
Function page.settings.installdir.onclick<br />
	nsDialogs::SelectFolderDialog &quot;Please select the destination folder&quot;<br />
	Pop $0<br />
	${If} $0 != &quot;error&quot;<br />
	    ${NSD_SetText} $NSD_InstallDir $0 ; This will automatically trigger page.settings.installdir.onchange<br />
	${EndIf}<br />
FunctionEnd<br />
<br />
Section &quot;&quot;<br />
SectionEnd</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>