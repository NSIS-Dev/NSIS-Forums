<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" Disable controls on custom page when checkbox checked, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  Disable controls on custom page when checkbox checked NSIS Discussion" />
	
	<title> Disable controls on custom page when checkbox checked [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  Disable controls on custom page when checkbox checked</div>
<hr />
<div class="pda"><a href="t-302522.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=302522">Disable controls on custom page when checkbox checked</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">kporter20</div><div class="date">28th January 2009, 14:54</div></div><div class="posttext">I have created a custom page and as part of this page I want 2 textboxes to disable if a checkbox is checked and then enable if the checkbox is unchecked. <br />
<br />
I have tried techniques described the user manual but have had no joy.<br />
<br />
I want to do this in the nsDialogsPageCheckChange function.<br />
<br />
Here is my installer script:<br />
<br />
# This is a test of the MSSQL OLEDB plugin that can be used to connect to a SQL Server instance<br />
# and execute commands and scripts<br />
<br />
# The installer includes a custom page which allows you to enter the network name of the sql server,<br />
# whether windows authentication is used or not and a username and password if required<br />
<br />
# This custom page also includes a Skip button if the user does not know this details<br />
<br />
Name &quot;MSSQL:OLEDB Plugin Test&quot;<br />
outfile 'customtest.exe'<br />
<br />
!include 'MUI.nsh'<br />
!include 'nsDialogs.nsh'<br />
<br />
Var Dialog<br />
Var Label<br />
Var Text<br />
Var Check<br />
<br />
!insertmacro MUI_LANGUAGE &quot;English&quot;<br />
page custom CustomPage nsDialogsPageLeave<br />
<br />
Function customPage<br />
   nsDialogs::Create /NOUNLOAD 1018<br />
<br />
   Pop $Dialog<br />
<br />
   ${If} $Dialog == error<br />
	Abort<br />
   ${EndIf}<br />
   <br />
   ; put controls onto the custom page<br />
   ; parameters - x, y, width, height and text<br />
   ${NSD_CreateLabel} 0 0 100% 20u &quot;Enter the details for your SQL Server login below and then click Next to create an EmployerSafe database on the specified SQL Server.&quot;<br />
   Pop $Label<br />
   <br />
   ${NSD_CreateLabel} 0 20u 100% 20u &quot;If you do not know these details, click on Skip and request that an IT administrator creates the Employersafe database using the supplied database creation script.&quot;<br />
   Pop $Label<br />
<br />
   ${NSD_CreateLabel} 0 60u 30% 10u &quot;SQL Server machine name:&quot;<br />
   Pop $Label<br />
   <br />
   ${NSD_CreateText} 90u 60u 50% 13u &quot;&quot;<br />
   Pop $Text<br />
   <br />
   ${NSD_CreateLabel} 0 80u 30% 10u &quot;Authentication details:&quot;<br />
   Pop $Label<br />
<br />
   ${NSD_CreateCheckBox} 90u 80u 70% 10u &quot;Use Windows Authentication&quot;<br />
   Pop $Check<br />
   ${NSD_OnChange} $Check nsDialogsPageCheckChange<br />
   <br />
   ${NSD_CreateLabel} 0 95u 30% 10u &quot;Username:&quot;<br />
   Pop $Label<br />
<br />
   ${NSD_CreateText} 90u 95u 50% 13u &quot;&quot;<br />
   Pop $Text<br />
<br />
   ${NSD_CreateLabel} 0 110u 30% 10u &quot;Password:&quot;<br />
   Pop $Label<br />
<br />
   ${NSD_CreateText} 90u 110u 50% 13u &quot;&quot;<br />
   Pop $Text<br />
<br />
   nsDialogs::Show<br />
<br />
FunctionEnd<br />
<br />
Function nsDialogsPageCheckChange<br />
   ; Unchecked = 0, Checked = 1<br />
   ${NSD_GetState} $Check $1<br />
<br />
   ${If} $Check == 0<br />
         ; enable username and password textboxes<br />
   ${Else}<br />
         ; disable username and password textboxes<br />
   ${EndIf}<br />
FunctionEnd<br />
<br />
Function nsDialogsPageLeave<br />
<br />
   ${NSD_GetText} $Text $0<br />
   MessageBox MB_OK &quot;You typed:$\n$\n$0&quot;<br />
   <br />
   ; Unchecked = 0, Checked = 1<br />
   ${NSD_GetState} $Check $1<br />
   MessageBox MB_OK &quot;$1&quot;<br />
FunctionEnd<br />
<br />
Section &quot;&quot;<br />
SectionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Red Wine</div><div class="date">28th January 2009, 15:16</div></div><div class="posttext">SendMessage<br />
<br />
http://nsis.sourceforge.net/Docs/Chapter4.html#4.9.14.10</div></div><hr />


<div class="post"><div class="posttop"><div class="username">kporter20</div><div class="date">28th January 2009, 15:21</div></div><div class="posttext">How would I use this to enable/disable the text boxes on my custom page when the check box is checked or unchecked?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Animaether</div><div class="date">29th January 2009, 00:04</div></div><div class="posttext">yeah, I'm not sure why he's pointing you to SendMessage :)<br />
<br />
There's a few things to fix in your script first...<br />
<br />
1. Make sure each control you interact with has its handle in a unique variable.<br />
<br />
<br />
Var LabelUsername<br />
Var TextUsername<br />
Var LabelPassword<br />
Var TextPassword<br />
<br />
; ...<br />
<br />
${NSD_CreateLabel} 0 95u 30% 10u &quot;Username:&quot;<br />
Pop $LabelUsername<br />
<br />
${NSD_CreateText} 90u 95u 50% 13u &quot;&quot;<br />
Pop $TextUsername<br />
<br />
${NSD_CreateLabel} 0 110u 30% 10u &quot;Password:&quot;<br />
Pop $LabelPassword<br />
<br />
${NSD_CreateText} 90u 110u 50% 13u &quot;&quot;<br />
Pop $TextPassword<br />
<br />
<br />
2. Checkboxes don't react to OnChange, only to OnClick (I don't know why)<br />
<br />
${NSD_CreateCheckBox} 90u 80u 70% 10u &quot;Use Windows Authentication&quot;<br />
Pop $Check<br />
${NSD_OnClick} $Check nsDialogsPageCheckChange<br />
<br />
<br />
3. The state of your $Check checkbox is output into $1, so that's what you need to test - not $Check<br />
<br />
<br />
4. Use EnableWindow &lt;handle&gt; &lt;0|1&gt; to disable/enable the controls<br />
<br />
${If} $1 == 0<br />
; enable username and password textboxes<br />
	EnableWindow $LabelUsername 1<br />
	EnableWindow $TextUsername 1<br />
	EnableWindow $LabelPassword 1<br />
	EnableWindow $TextPassword 1<br />
${Else}<br />
; disable username and password textboxes<br />
	EnableWindow $LabelUsername 0<br />
	EnableWindow $TextUsername 0<br />
	EnableWindow $LabelPassword 0<br />
	EnableWindow $TextPassword 0<br />
${EndIf}<br />
<br />
<br />
With those changes, you should be all set :)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">kporter20</div><div class="date">29th January 2009, 08:49</div></div><div class="posttext">Thanks for the detailed reply and example.<br />
<br />
I will try this later. :)<br />
<br />
EDIT: This works perfectly! Thanks.</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>