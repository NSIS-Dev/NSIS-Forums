<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" how to change the browse button text of the dirrequest control, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  how to change the browse button text of the dirrequest control NSIS Discussion" />
	
	<title> how to change the browse button text of the dirrequest control [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  how to change the browse button text of the dirrequest control</div>
<hr />
<div class="pda"><a href="t-262257.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=262257">how to change the browse button text of the dirrequest control</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">easthero</div><div class="date">22nd December 2006, 03:35</div></div><div class="posttext">in custom page,I create a dirrequest control,<br />
the browse button has no text,only three dot,I want to change these three dot to text,such as &quot;click here to browser&quot;<br />
<br />
thanks</div></div><hr />


<div class="post"><div class="posttop"><div class="username">kichik</div><div class="date">22nd December 2006, 11:48</div></div><div class="posttext">You'll have to work some magic to do that, since InstallOptions doesn't support it. You'll have to call SetWindowPos using the System plug-in to set the button's size and then use SendMessage with WM_SETTEXT to set the new text.<br />
<br />
These should help:<br />
<br />
http://nsis.sourceforge.net/Moving_install_window_to_a_corner_of_the_screen<br />
http://nsis.sourceforge.net/Change_caption_of_installer_at_runtime</div></div><hr />


<div class="post"><div class="posttop"><div class="username">NewDevToNsis</div><div class="date">3rd January 2007, 21:01</div></div><div class="posttext">Hi Kichik or anybody,<br />
<br />
Sorry I am very new to write NSIS scripts. Iâ€™ve the same problem, too. Can anybody help?<br />
<br />
I donâ€™t quite understand what is â€œcall SetWindowPos using the System plug-in to set the button's sizeâ€ and â€œuse SendMessage with WM_SETTEXT to set the new textâ€<br />
<br />
I only know how to use SetWindowPos for setting the Window position? How can it set the button position?<br />
<br />
According to the example provided in http://nsis.sourceforge.net/Change_...ller_at_runtime<br />
Like:<br />
<br />
SendMessage $HWNDPARENT ${WM_SETTEXT} 0 &quot;STR:We are waiting... [Section 1]&quot;<br />
<br />
The SendMessage and WM_SETTEXT only set the heading of the window. I try to set it to DirRequest, it is not working.<br />
<br />
Would you mind explaining more about how to do so?<br />
<br />
Thanks for your help.<br />
 :cry:</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Red Wine</div><div class="date">3rd January 2007, 22:46</div></div><div class="posttext">Alternatively you may use the function BrowseForFolder somehow like in the following example.<br />
The function BrowseForFolder spreads around the forum with some examples as well.<br />
<br />
Name &quot;My Application&quot;<br />
OutFile 'test.exe'<br />
LicenseData '${NSISDIR}\License.txt'<br />
LicenseBkColor 0xFFFFFF<br />
ShowInstDetails show<br />
<br />
!include WinMessages.nsh<br />
<br />
Page License<br />
Page Custom CustomCreate CustomLeave<br />
Page InstFiles<br />
<br />
Section &quot;boo&quot;<br />
        SetOutPath '$INSTDIR'<br />
SectionEnd<br />
<br />
Function CustomCreate<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Settings' 'NumFields' '2'<br />
<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Type' 'Text'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Left' '5'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Top' '36'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Right' '166'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Bottom' '48'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'Flags' 'READONLY|NOWORDWRAP'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'State' ''<br />
         <br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Type' 'Button'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Left' '174'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Top' '35'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Right' '-5'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Bottom' '49'<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Flags' 'Notify'<br />
;         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'State' ''<br />
         WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 2' 'Text' 'This button really sucks'<br />
         <br />
         push $0<br />
         InstallOptions::InitDialog /NOUNLOAD '$PLUGINSDIR\custom.ini'<br />
         pop $0<br />
         ReadINIStr $1 &quot;$PLUGINSDIR\custom.ini&quot; &quot;Field 1&quot; &quot;HWND&quot;<br />
         SetCtlColors $1 0x000000 0xFFFFFF<br />
         InstallOptions::Show '$PLUGINSDIR\custom.ini'<br />
         pop $0<br />
         pop $0<br />
FunctionEnd<br />
<br />
Function CustomLeave<br />
        ReadIniStr $0 '$PLUGINSDIR\custom.ini' 'Settings' 'State'<br />
        StrCmp $0 '2' 0 next<br />
<br />
        Call BrowseForFolder<br />
        StrCmp $3 '' _abort<br />
<br />
        WriteIniStr '$PLUGINSDIR\custom.ini' 'Field 1' 'State' '$3'<br />
        ReadIniStr $0 '$PLUGINSDIR\custom.ini' 'Field 1' 'HWND'<br />
        SendMessage $0 ${WM_SETTEXT} 0 'STR:$3'        <br />
     _abort:<br />
        Abort<br />
<br />
     next:<br />
        ReadIniStr $0 '$PLUGINSDIR\custom.ini' 'Field 1' 'State'<br />
        StrCmp $0 '' 0 exit<br />
        MessageBox MB_OK|MB_ICONEXCLAMATION &quot;Plz browse for a folder&quot;<br />
        Abort<br />
     exit:<br />
        StrCpy '$INSTDIR' '$0'<br />
FunctionEnd<br />
<br />
Function BrowseForFolder<br />
        System::Store &quot;s r9&quot;<br />
<br />
        ; BrowseInfo structure<br />
        System::Call '*(i $HWNDPARENT, i 0, t &quot;&quot;, t r9, i 0x45, i 0, i, i) i.r1'<br />
        System::Call 'shell32::SHBrowseForFolderA(i r1) i.r2'<br />
        System::Free $1<br />
<br />
        ; now we should determine folder name from given PIDL<br />
        System::Call 'shell32::SHGetPathFromIDListA(i $2, t &quot;&quot; r3)'<br />
<br />
        ; IMalloc-&gt;Free work<br />
        System::Call 'shell32::SHGetMalloc(*i . r4)'    ; Get the pointer to interface<br />
        ; IMalloc-&gt;Free call<br />
        System::Call '$4-&gt;5(i r2)'<br />
        ; IMalloc-&gt;Release<br />
        System::Call '$4-&gt;2()'<br />
<br />
        System::Store &quot;p3 l&quot;<br />
FunctionEnd<br />
<br />
Function .onInit<br />
         InitPluginsDir<br />
         GetTempFileName $0<br />
         Rename $0 '$PLUGINSDIR\custom.ini'<br />
FunctionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Afrow UK</div><div class="date">4th January 2007, 00:17</div></div><div class="posttext">I did this quite a while ago:<br />
http://forums.winamp.com/showthread.php?threadid=245878<br />
<br />
-Stu</div></div><hr />


<div class="post"><div class="posttop"><div class="username">NewDevToNsis</div><div class="date">5th January 2007, 17:05</div></div><div class="posttext">Thank you. It's working. :D</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>