<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" How to check if cancel button pressed in nsDialogs, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  How to check if cancel button pressed in nsDialogs NSIS Discussion" />
	
	<title> How to check if cancel button pressed in nsDialogs [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  How to check if cancel button pressed in nsDialogs</div>
<hr />
<div class="pda"><a href="t-335664.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=335664">How to check if cancel button pressed in nsDialogs</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">howzer</div><div class="date">6th October 2011, 11:51</div></div><div class="posttext">Hello,<br />
I've created a cutom page using nsDialogs. How would I check if the user has pressed the cancel button. At the moment I have a condition to make sure a text box has been filled and not left blank. If it is left blank and the user decides to cancel then and there, the same check condition gets executed and there is no way of aborting the installer. Many Thanks.<br />
<br />
Function ConnectionDialogShowAndLeave<br />
    Push $R0<br />
<br />
    ${Do}<br />
        !insertmacro MUI_HEADER_TEXT &quot;Registry Connection Details&quot; &quot;Please provide information regarding your connection string and database settings.&quot;<br />
        nsDialogs::Create 1018<br />
        Pop $0  <br />
        ${If} $0 == error<br />
            Abort<br />
        ${EndIf}<br />
   <br />
        # LHApp Connection String<br />
        ${NSD_CreateLabel} 0 12 25% 12u &quot;Database Connection:&quot;<br />
        ${NSD_CreateText} 25% 10 75% 12u &quot;$ConnectionString&quot;<br />
        Pop $Text<br />
        GetFunctionAddress $0 GetText<br />
        nsDialogs::OnChange $Text $0<br />
    <br />
        # DBMS Radio buttons<br />
        ${NSD_CreateGroupBox} 0 40 100% 45u  &quot;Database Type&quot;<br />
        ${NSD_CreateRadioButton} 10 60 50% 12u &quot;Oracle&quot;<br />
        Pop $Button1<br />
        GetFunctionAddress $0 GetButtonState<br />
        nsDialogs::OnClick $Button1 $0<br />
    <br />
        ${NSD_CreateRadioButton} 10 80 50% 12u &quot;SQL-Server&quot;<br />
        Pop $Button2<br />
        nsDialogs::OnClick $Button2 $0<br />
    <br />
        # Check the current value of DBMS and check the associated radio button<br />
        ${If} $DBMS == &quot;0&quot;<br />
            ${NSD_Check} $Button1<br />
        ${ElseIf} $DBMS == &quot;1&quot;<br />
            ${NSD_Check} $Button2<br />
        ${EndIf}   <br />
        nsDialogs::Show  <br />
    <br />
        # Set the correlating DBMS value<br />
        ${If} $Button1State == ${BST_CHECKED}<br />
            StrCpy $DBMS &quot;0&quot;   <br />
        ${ElseIf} $Button2State == ${BST_CHECKED}<br />
            StrCpy $DBMS &quot;1&quot; <br />
        ${EndIf}<br />
        <br />
        # Ensure connection string has been provided<br />
        ${If} $ConnectionString == &quot;&quot;<br />
            MessageBox MB_ICONEXCLAMATION &quot;Please provide the database connection.&quot; <br />
            ${Continue}<br />
        ${EndIf}<br />
            <br />
        # Ensure DBMS has been selected<br />
        ${If} $DBMS != &quot;0&quot; <br />
        ${AndIf} $DBMS != &quot;1&quot;<br />
            MessageBox MB_ICONEXCLAMATION &quot;Please choose a database type.&quot;<br />
            ${Continue}<br />
        ${EndIf}<br />
                   <br />
        # If we get here, then all details have been provided, so exit the loop<br />
       ${ExitDo}<br />
    ${Loop}<br />
<br />
    Pop $R0<br />
FunctionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Afrow UK</div><div class="date">6th October 2011, 12:06</div></div><div class="posttext">You are validating your fields incorrectly. You should be doing it in the page leave function where you can simply call Abort to return back to the page. The leave function is not called if they click Back or Cancel.<br />
<br />
Stu</div></div><hr />


<div class="post"><div class="posttop"><div class="username">howzer</div><div class="date">6th October 2011, 12:20</div></div><div class="posttext">If I wanted to return back to the page from another function, wouldn't I get a stack error? Wouldn't calling abort just leave the installer?</div></div><hr />


<div class="post"><div class="posttop"><div class="username">howzer</div><div class="date">6th October 2011, 12:31</div></div><div class="posttext">Ok I tried what you suggested and it worked! Thanks Stu. All this time I thought calling abort would just close the installer but in nsDialogs, it will just refresh the page?<br />
<br />
Function ConnectionDialogShow<br />
<br />
        !insertmacro MUI_HEADER_TEXT &quot;Registry Connection Details&quot; &quot;Please provide information regarding your connection string and database settings.&quot;<br />
        nsDialogs::Create 1018<br />
        Pop $0  <br />
        ${If} $0 == error<br />
            Abort<br />
        ${EndIf}<br />
   <br />
        # LHApp Connection String<br />
        ${NSD_CreateLabel} 0 12 25% 12u &quot;Database Connection:&quot;<br />
        ${NSD_CreateText} 25% 10 75% 12u &quot;$ConnectionString&quot;<br />
        Pop $Text<br />
        GetFunctionAddress $0 GetText<br />
        nsDialogs::OnChange $Text $0<br />
    <br />
        # DBMS Radio buttons<br />
        ${NSD_CreateGroupBox} 0 40 100% 45u  &quot;Database Type&quot;<br />
        ${NSD_CreateRadioButton} 10 60 50% 12u &quot;Oracle&quot;<br />
        Pop $Button1<br />
        GetFunctionAddress $0 GetButtonState<br />
        nsDialogs::OnClick $Button1 $0<br />
    <br />
        ${NSD_CreateRadioButton} 10 80 50% 12u &quot;SQL-Server&quot;<br />
        Pop $Button2<br />
        nsDialogs::OnClick $Button2 $0<br />
        <br />
        # Check the current value of DBMS and check the associated radio button<br />
        ${If} $DBMS == &quot;0&quot;<br />
            ${NSD_Check} $Button1<br />
        ${ElseIf} $DBMS == &quot;1&quot;<br />
            ${NSD_Check} $Button2<br />
        ${EndIf}   <br />
        nsDialogs::Show  <br />
             <br />
FunctionEnd<br />
<br />
Function ConnectionDialogLeave<br />
    ${NSD_GetText} $Text $ConnectionString<br />
    ${NSD_GetState} $Button1 $Button1State<br />
    ${NSD_GetState} $Button2 $Button2State<br />
    <br />
    # Set the correlating DBMS value<br />
    ${If} $Button1State == ${BST_CHECKED}<br />
        StrCpy $DBMS &quot;0&quot;   <br />
    ${ElseIf} $Button2State == ${BST_CHECKED}<br />
        StrCpy $DBMS &quot;1&quot; <br />
    ${EndIf}<br />
    <br />
    # Ensure connection string has been provided<br />
    ${If} $ConnectionString == &quot;&quot;<br />
        MessageBox MB_ICONEXCLAMATION &quot;Please provide the database connection.&quot; <br />
        Abort<br />
    ${EndIf}<br />
            <br />
    # Ensure DBMS has been selected<br />
    ${If} $DBMS != &quot;0&quot; <br />
    ${AndIf} $DBMS != &quot;1&quot;<br />
        MessageBox MB_ICONEXCLAMATION &quot;Please choose a database type.&quot;<br />
        Abort<br />
    ${EndIf}<br />
FunctionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Afrow UK</div><div class="date">6th October 2011, 14:58</div></div><div class="posttext">Read section 4.5.3 Callbacks in the manual.<br />
<br />
Stu</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>