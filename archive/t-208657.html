<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" How Do You Use TrimNewLine to Remove All $\r$\n at The End of a File?, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  How Do You Use TrimNewLine to Remove All $\r$\n at The End of a File? NSIS Discussion" />
	
	<title> How Do You Use TrimNewLine to Remove All $\r$\n at The End of a File? [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  How Do You Use TrimNewLine to Remove All $\r$\n at The End of a File?</div>
<hr />
<div class="pda"><a href="t-208657.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=208657">How Do You Use TrimNewLine to Remove All $\r$\n at The End of a File?</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">23rd February 2005, 22:04</div></div><div class="posttext">I am having a problem with this function. I really do not know how to apply it, yet I think I need it. Basically I am trying to open a file which might look like the following.<br />
78. blah<br />
79. last line<br />
80.<br />
81.<br />
82.I need to delete lines 80, 81 and 82. In the end this file should look like this78. blah<br />
79. last lineI am really not understanding where to place this function or how to call it. I am using file open for some other actions so do I place this call after fileopen? I've tried many things and I feel am getting no where. Can someone please help point out to me how to do what it is I am trying to do?<br />
<br />
Also I searched the forums and help files but no real good examples exist on how exactly this function could be used in a real world scenario. Also, I cannot pinpoint the last word found in the file because it could be anything. Also, I am not trying to replace a word or character but am trying to simply clean up the end of the file.<br />
<br />
Can someone help me out. Thank you for understanding and for your time.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 01:42</div></div><div class="posttext">I don't wish to start a new thread but thought it would be nice if someone with the skills created a file cleanup function. i.e.<br />
<br />
1. Remove consecutive empty lines until only one line remains.<br />
2. Remove trailing spaces from a file line for line.<br />
3. etc<br />
<br />
Just an idea. Just thinking. Thanks to the wo/man who can get this done if somoene decides to take this on.<br />
<br />
Thanks again!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 08:45</div></div><div class="posttext">Name 'Output'<br />
OutFile 'Output.exe'<br />
<br />
!include &quot;TextFunc.nsh&quot;<br />
!insertmacro LineFind<br />
!insertmacro LineRead<br />
!insertmacro TrimNewLines<br />
<br />
!define Input &quot;C:\Input.txt&quot;<br />
!define Output &quot;C:\Output.txt&quot;<br />
<br />
Section<br />
	StrCpy $0 -1<br />
<br />
	loop:<br />
	${LineRead} &quot;${Input}&quot; &quot;$0&quot; $1<br />
	${TrimNewLines} &quot;$1&quot; $1<br />
	StrCmp $1 &quot;&quot; 0 +3<br />
	IntOp $0 $0 - 1<br />
	goto loop<br />
<br />
	${LineFind} &quot;${Input}&quot; &quot;${Output}&quot; &quot;$0:-1&quot; &quot;CallbackLineFind&quot;<br />
SectionEnd<br />
<br />
Function CallbackLineFind<br />
	StrCmp $R0 FirstLine SkipWrite<br />
	StrCpy $R0 FirstLine<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
Script used header (http://forums.winamp.com/attachment.php?s=&amp;postid=1590305)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 09:20</div></div><div class="posttext">Thanks Instructor,<br />
<br />
Very cool. I worked with it and managed to copy contents from one file into another and even into itself with trimmed lines. Thank you. Can this function be smart enough to scan a file from top to bottom and remove all consecutive empty lines (not just empty lines with something above or beneath it)?<br />
<br />
i.e.79. txt<br />
80. <br />
81. <br />
82. nsi<br />
83.<br />
84.<br />
85.<br />
86. wow a lot of empty space<br />
87.<br />
88.<br />
85. nsh<br />
<br />
<br />
to this<br />
<br />
79. txt<br />
80.<br />
81. nsi<br />
82.<br />
83. wow a lot of empty space<br />
84.<br />
85. nsh<br />
<br />
Basically just removing an empty line if an empty line exist beneath or above it? I haven't got the logic to do it myself. The farthest I got was replacing all lines or removing all $\r$\n to the point where everything ended up smashed together...<br />
<br />
Thanks a million for the function Instructor.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 09:50</div></div><div class="posttext">Name 'Output'<br />
OutFile 'Output.exe'<br />
<br />
!include &quot;TextFunc.nsh&quot;<br />
!insertmacro LineFind<br />
!insertmacro TrimNewLines<br />
<br />
!define Input &quot;C:\Input.txt&quot;<br />
!define Output &quot;C:\Output.txt&quot;<br />
<br />
Section<br />
	${LineFind} &quot;${Input}&quot; &quot;${Output}&quot; &quot;1:-1&quot; &quot;CallbackLineFind&quot;<br />
SectionEnd<br />
<br />
Function CallbackLineFind<br />
	${TrimNewLines} &quot;$R9&quot; $1<br />
	StrCmp $1 &quot;&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	StrCpy $R0 FoundEmpty<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
Script used header (http://forums.winamp.com/attachment.php?s=&amp;postid=1590305)</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 10:12</div></div><div class="posttext">You're the man... I've been up all night trying to figure this out and my skills with NSIS is still young. I have just one last question. I know how to work with functions a bit but can you help me figure out how to turn your work into some very simple insertmacros?<br />
<br />
i.e.!insertmacro CleanLines $EXEDIR.test.txt ;clean all lines<br />
!insertmacro CleanEOF   $EXEDIR.test.txt ;clean last lineOR!insertmacro CleanLines CleanEOF $EXEDIR.test.txt ;clean all lines including the end.<br />
<br />
The reason I ask is because it seems I would have to duplicate the code for every file I need cleaned. This wouldn't be a problem if you gave up now as you've put me way ahead of myself (thank you). Also, once this is done and tightened up please share it with the world on the archives. Call it &quot;File Cleaner Version 1.0&quot;.<br />
<br />
1.0 Because I am certain others will need deeper cleaning :)<br />
<br />
Thanks a million again Instructor, your work is priceless!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 11:48</div></div><div class="posttext">Name 'Output'<br />
OutFile 'Output.exe'<br />
<br />
!include &quot;TextFunc.nsh&quot;<br />
!insertmacro LineFind<br />
!insertmacro LineRead<br />
!insertmacro TrimNewLines<br />
<br />
Function CleanLines<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $R0<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;1:-1&quot; &quot;CallbackCleanLines&quot;<br />
<br />
	Pop $R0<br />
	Pop $1<br />
	Pop $0<br />
FunctionEnd<br />
<br />
Function CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $1<br />
	StrCmp $1 &quot;&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	StrCpy $R0 FoundEmpty<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
<br />
<br />
Function CleanEOF<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $2<br />
	Push $3<br />
	Push $R0<br />
<br />
	StrCpy $2 -1<br />
<br />
	loop:<br />
	${LineRead} &quot;$0&quot; &quot;$2&quot; $3<br />
	${TrimNewLines} &quot;$3&quot; $3<br />
	StrCmp $3 &quot;&quot; 0 +3<br />
	IntOp $2 $2 - 1<br />
	goto loop<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;$2:-1&quot; &quot;CallbackCleanEOF&quot;<br />
<br />
	Pop $R0<br />
	Pop $3<br />
	Pop $2<br />
	Pop $1<br />
	Pop $0<br />
FunctionEnd<br />
<br />
Function CallbackCleanEOF<br />
	StrCmp $R0 FirstLine SkipWrite<br />
	StrCpy $R0 FirstLine<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
<br />
<br />
<br />
!macro CleanLines &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanLines<br />
!macroEnd<br />
<br />
!macro CleanEOF &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanEOF<br />
!macroEnd<br />
<br />
!macro CleanLines+CleanEOF &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanLines<br />
<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanEOF<br />
!macroEnd<br />
<br />
<br />
Section<br />
	!insertmacro CleanLines+CleanEOF &quot;C:\Input1.txt&quot; &quot;C:\Output1.txt&quot;<br />
	!insertmacro CleanLines+CleanEOF &quot;C:\Input2.txt&quot; &quot;C:\Output2.txt&quot;<br />
SectionEnd<br />
<br />
Script used header (http://forums.winamp.com/attachment.php?s=&amp;postid=1590305)<br />
<br />
If you want you can share it.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 12:05</div></div><div class="posttext">Wow, Amazing!<br />
<br />
This is a must share script. Super fantastic and fabulous work. Was working on my version by modifying your scripts a little but I kept getting stuck on an evil loop :(<br />
<br />
This is awesome, if you wish I will put it in the archives on your behalf but I'd suggest you best put it under your belt ;) It works without hiccups right out of the box. Awesome!<br />
<br />
Thanks for your help Instructor, I know others will find it useful! Keep up the great work!<br />
<br />
Best Regards!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 13:32</div></div><div class="posttext">I'm glad to help you.<br />
<br />
For empty files support:	...<br />
	${LineRead} &quot;$0&quot; &quot;$2&quot; $3<br />
	IfErrors end<br />
	...<br />
<br />
	...<br />
	end:<br />
	Pop $R0<br />
	...<br />
<br />
<br />
To remove spaces and &quot;tab&quot; at the end of lines:Function CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	StrCpy $1 $R9 1 -1<br />
	StrCmp $1 &quot; &quot; +2<br />
	StrCmp $1 &quot;	&quot; 0 +3<br />
	StrCpy $R9 $R9 -1<br />
	goto -4<br />
	StrCpy $R9 &quot;$R9$\r$\n&quot;<br />
<br />
	StrCmp $R9 &quot;$\r$\n&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	...</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 14:03</div></div><div class="posttext">So replace thisFunction CleanEOF<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $2<br />
	Push $3<br />
	Push $R0<br />
<br />
	StrCpy $2 -1<br />
<br />
	loop:<br />
	${LineRead} &quot;$0&quot; &quot;$2&quot; $3<br />
<br />
	${TrimNewLines} &quot;$3&quot; $3<br />
	StrCmp $3 &quot;&quot; 0 +3<br />
	IntOp $2 $2 - 1<br />
	goto loop<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;$2:-1&quot; &quot;CallbackCleanEOF&quot;<br />
<br />
	Pop $R0<br />
	Pop $3<br />
	Pop $2<br />
	Pop $1<br />
	Pop $0<br />
FunctionEndWith the following and this enables empty file support?Function CleanEOF<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $2<br />
	Push $3<br />
	Push $R0<br />
<br />
	StrCpy $2 -1<br />
<br />
	loop:<br />
	${LineRead} &quot;$0&quot; &quot;$2&quot; $3<br />
IfErrors end<br />
	${TrimNewLines} &quot;$3&quot; $3<br />
	StrCmp $3 &quot;&quot; 0 +3<br />
	IntOp $2 $2 - 1<br />
	goto loop<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;$2:-1&quot; &quot;CallbackCleanEOF&quot;<br />
end:<br />
	Pop $R0<br />
	Pop $3<br />
	Pop $2<br />
	Pop $1<br />
	Pop $0<br />
FunctionEndWhat exactly does this mean? In case I try to clean up an empty file, the error flag is set and the actions are skipped?<br />
<br />
To enable tab and trailing space support I replace thisFunction CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $1<br />
	StrCmp $1 &quot;&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	StrCpy $R0 FoundEmpty<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEndWith this?Function CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	StrCpy $1 $R9 1 -1<br />
	StrCmp $1 &quot; &quot; +2<br />
	StrCmp $1 &quot;	&quot; 0 +3<br />
	StrCpy $R9 $R9 -1<br />
	goto -4<br />
	StrCpy $R9 &quot;$R9$\r$\n&quot;<br />
<br />
	StrCmp $R9 &quot;$\r$\n&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
<br />
	${TrimNewLines} &quot;$R9&quot; $1<br />
	StrCmp $1 &quot;&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	StrCpy $R0 FoundEmpty<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEndI think I might have messed up this function on this paste. Does it seem correct? I will give these functions a stab in a minute. Just confirming in case I start to steer off early.<br />
<br />
FileCleanUp time :) Yes!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 14:23</div></div><div class="posttext">In case I try to clean up an empty file, the error flag is set and the actions are skipped?Yes.<br />
<br />
<br />
Name 'Output'<br />
OutFile 'Output.exe'<br />
<br />
!include &quot;TextFunc.nsh&quot;<br />
!insertmacro LineFind<br />
!insertmacro LineRead<br />
!insertmacro TrimNewLines<br />
<br />
Function CleanLines<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $R0<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;1:-1&quot; &quot;CallbackCleanLines&quot;<br />
<br />
	Pop $R0<br />
	Pop $1<br />
	Pop $0<br />
FunctionEnd<br />
<br />
Function CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	StrCpy $1 $R9 1 -1<br />
	StrCmp $1 &quot; &quot; +2<br />
	StrCmp $1 &quot;	&quot; 0 +3<br />
	StrCpy $R9 $R9 -1<br />
	goto -4<br />
	StrCpy $R9 &quot;$R9$\r$\n&quot;<br />
<br />
	StrCmp $R9 &quot;$\r$\n&quot; +3<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCmp $R0 FoundEmpty SkipWrite<br />
	StrCpy $R0 FoundEmpty<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
<br />
<br />
Function CleanEOF<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $2<br />
	Push $3<br />
	Push $R0<br />
<br />
	StrCpy $2 -1<br />
<br />
	loop:<br />
	${LineRead} &quot;$0&quot; &quot;$2&quot; $3<br />
	IfErrors end<br />
<br />
	${TrimNewLines} &quot;$3&quot; $3<br />
	StrCmp $3 &quot;&quot; 0 +3<br />
	IntOp $2 $2 - 1<br />
	goto loop<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;$2:-1&quot; &quot;CallbackCleanEOF&quot;<br />
<br />
	end:<br />
	Pop $R0<br />
	Pop $3<br />
	Pop $2<br />
	Pop $1<br />
	Pop $0<br />
FunctionEnd<br />
<br />
Function CallbackCleanEOF<br />
	StrCmp $R0 FirstLine SkipWrite<br />
	StrCpy $R0 FirstLine<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	goto end<br />
<br />
	SkipWrite:<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
<br />
<br />
<br />
!macro CleanLines &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanLines<br />
!macroEnd<br />
<br />
!macro CleanEOF &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanEOF<br />
!macroEnd<br />
<br />
!macro CleanLines+CleanEOF &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanLines<br />
<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanEOF<br />
!macroEnd<br />
<br />
<br />
Section<br />
	!insertmacro CleanLines+CleanEOF &quot;C:\Input1.txt&quot; &quot;C:\Output1.txt&quot;<br />
	!insertmacro CleanLines+CleanEOF &quot;C:\Input2.txt&quot; &quot;C:\Output2.txt&quot;<br />
SectionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">24th February 2005, 15:01</div></div><div class="posttext">Powerful. Thank you for sharing this with me and thank you so much for your time and effort in creating a really great file clean up function. The archives thirsty for this one :)<br />
<br />
Your work is undoubtedly appreciated and I thank you again!<br />
<br />
It works great!:up: :up: :up: :up: :up:</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">24th February 2005, 17:52</div></div><div class="posttext">vbgunz, it's not necessary so much thanks :).<br />
I had an idea and wrote new compact code. Now if you use CleanLines function you don't need to use CleanEOF. If you found it interesting... :<br />
Name 'Output'<br />
OutFile 'Output.exe'<br />
<br />
!include &quot;TextFunc.nsh&quot;<br />
!insertmacro LineFind<br />
!insertmacro TrimNewLines<br />
<br />
Function CleanLines<br />
	Exch $1<br />
	Exch<br />
	Exch $0<br />
	Exch<br />
	Push $R0<br />
<br />
	${LineFind} &quot;$0&quot; &quot;$1&quot; &quot;1:-1&quot; &quot;CallbackCleanLines&quot;<br />
<br />
	Pop $R0<br />
	Pop $1<br />
	Pop $0<br />
FunctionEnd<br />
<br />
Function CallbackCleanLines<br />
	${TrimNewLines} &quot;$R9&quot; $R9<br />
	StrCpy $1 $R9 1 -1<br />
	StrCmp $1 &quot; &quot; +2<br />
	StrCmp $1 &quot;	&quot; 0 +3<br />
	StrCpy $R9 $R9 -1<br />
	goto -4<br />
	StrCpy $R9 &quot;$R9$\r$\n&quot;<br />
<br />
	StrCmp $R9 &quot;$\r$\n&quot; +5<br />
	StrCmp $R0 FoundEmpty 0 end<br />
	FileWrite $R7 &quot;$\r$\n&quot;<br />
	StrCpy $R0 &quot;&quot;<br />
	goto end<br />
<br />
	StrCpy $R0 FoundEmpty<br />
	StrCpy $0 SkipWrite<br />
<br />
	end:<br />
	Push $0<br />
FunctionEnd<br />
<br />
<br />
!macro CleanLines &quot;_INPUT&quot; &quot;_OUTPUT&quot;<br />
	Push &quot;${_INPUT}&quot;<br />
	Push &quot;${_OUTPUT}&quot;<br />
	Call CleanLines<br />
!macroEnd<br />
<br />
<br />
Section<br />
	!insertmacro CleanLines &quot;C:\Input1.txt&quot; &quot;C:\Output1.txt&quot;<br />
	!insertmacro CleanLines &quot;C:\Input2.txt&quot; &quot;C:\Output2.txt&quot;<br />
SectionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">25th February 2005, 04:14</div></div><div class="posttext">Hello Instructor,<br />
<br />
You can never thank someone enough when they do something for you in which you cannot do for youself :) Sorry, its custom for me to extend my thanks and gratitude.<br />
<br />
The last piece of code you've shared fails to remove the very last $\r$\n and so one last blank line is left behind. Other than this, it does clean up everything else.<br />
<br />
1. consecutive empty lines<br />
2. trailing white spaces on end of lines<br />
3. End of file but leaves one blank line behind<br />
4. Skip empty files<br />
<br />
Great work so far in compacting the code although I am sticking with your second to last post. That one is perfect ;)<br />
<br />
Thanks again Instructor. I appreciate your time!</div></div><hr />


<div class="post"><div class="posttop"><div class="username">Instructor</div><div class="date">25th February 2005, 08:35</div></div><div class="posttext">The last piece of code you've shared fails to remove the very last $\r$\n<br />
Yes, so was conceived. If you don't need &quot;last $\r$\n&quot; use CleanEOF after.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">vbgunz</div><div class="date">25th February 2005, 10:06</div></div><div class="posttext">My apologies Instructor. Thank you for your help. Major thanks will be written in my script ;)</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>