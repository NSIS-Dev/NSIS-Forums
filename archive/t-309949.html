<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
	<meta name="keywords" content=" WMI via System plugin - query BIOS serial number, media player, mp3 player, music player, ipod sync, multimedia player, player, winamp, audio player" />
	<meta name="description" content="[Archive]  WMI via System plugin - query BIOS serial number NSIS Discussion" />
	
	<title> WMI via System plugin - query BIOS serial number [Archive]  - Winamp Forums</title>
	<link rel="stylesheet" type="text/css" href="archive.css" />
</head>
<body>
<div class="pagebody">
<div id="navbar">Winamp Forums &gt; Developer Center &gt; <a href="f-65.html">NSIS Discussion</a> &gt;  WMI via System plugin - query BIOS serial number</div>
<hr />
<div class="pda"><a href="t-309949.html?login=1" rel="nofollow">Log in</a></div>
<p class="largefont">Try the Internet Archive: <a href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=309949">WMI via System plugin - query BIOS serial number</a></p>
<hr />

<div class="post"><div class="posttop"><div class="username">f0rt</div><div class="date">13th August 2009, 19:58</div></div><div class="posttext">The following code demonstrates the usage of the Windows Management Instrumentation (WMI) only via the System plugin.<br />
The sample code displays the BIOS serial number in a message box.<br />
<br />
I have not yet tried to remotely access computers via this script. Furthermore I left out a call to the CoSetProxyBlanket function because it seems that is is not needed for querying the BIOS serial number.<br />
<br />
<br />
; Copyright (c) 2009 f0rt<br />
; Licensed under the zlib/libpng license (same as NSIS)<br />
;<br />
; Usage of Windows Management Instrumentation (WMI) only via the System plugin<br />
; Display BIOS serial number<br />
<br />
OutFile &quot;bios_sn.exe&quot;<br />
<br />
!include &quot;LogicLib.nsh&quot;<br />
<br />
!define CLSCTX_INPROC_SERVER 1<br />
!define CLSID_WbemLocator {4590f811-1d3a-11d0-891f-00aa004b2e24}<br />
!define IID_IWbemLocator {dc12a687-737f-11cf-884d-00aa004b2e24}<br />
<br />
!define RPC_C_AUTHN_LEVEL_DEFAULT   0<br />
!define RPC_C_IMP_LEVEL_IMPERSONATE 3<br />
<br />
!define EOAC_NONE                   0<br />
<br />
!define WBEM_FLAG_FORWARD_ONLY       0x20<br />
!define WBEM_FLAG_RETURN_IMMEDIATELY 0x10<br />
!define WBEM_INFINITE                0xffffffff<br />
<br />
; Query BIOS serial number via WMI and return it in $0<br />
Function bios_sn<br />
  ; Save registers<br />
  Push $1<br />
  Push $2<br />
  Push $3<br />
  Push $4<br />
  Push $5<br />
  Push $6<br />
  Push $7<br />
<br />
  ; Initialization of COM is done via OleInitialize in NSIS installer code<br />
  ; Set general COM security level<br />
  System::Call &quot;ole32::CoInitializeSecurity( \\<br />
        i 0, i -1, i 0, i 0, i ${RPC_C_AUTHN_LEVEL_DEFAULT}, \\<br />
        i ${RPC_C_IMP_LEVEL_IMPERSONATE}, i 0, i ${EOAC_NONE}, i 0) i.r1&quot;<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to initialize security: $1&quot;<br />
     Goto bios_sn_end<br />
  ${EndIf}<br />
  <br />
  ; Create IWbemLocator interface<br />
  System::Call &quot;ole32::CoCreateInstance( \\<br />
        g '${CLSID_WbemLocator}', i 0, \\<br />
        i ${CLSCTX_INPROC_SERVER}, \\<br />
        g '${IID_IWbemLocator}', *i .r2) i.r1&quot;<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to create IWebmLocator object: $1&quot;<br />
     Goto bios_sn_end<br />
  ${EndIf}<br />
<br />
  ; Call IWbemLocator-&gt;ConnectServer<br />
  System::Call &quot;$2-&gt;3(w 'ROOT\CIMV2', i 0, i 0, i 0, i 0, i 0, i 0, *i .r3) i.r1&quot;<br />
<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to connect: $1&quot;<br />
  ${Else}<br />
     ; Call IWbemServices-&gt;ExecQuery <br />
     System::Call &quot;$3-&gt;20(w 'WQL', w 'Select SerialNumber from Win32_BIOS', \\<br />
        ${WBEM_FLAG_FORWARD_ONLY} | ${WBEM_FLAG_RETURN_IMMEDIATELY}, \\<br />
        i 0, *i .r4) i.r1&quot;<br />
<br />
     ${If} $1 != 0<br />
         StrCpy $0 &quot;failed to query: $1 $3&quot;<br />
     ${Else}<br />
         ; Call IEnumWbemClassObject-&gt;Next<br />
         System::Call &quot;$4-&gt;4(i ${WBEM_INFINITE}, i 1, *i .r5, *i .r6) i.r1&quot;<br />
         ${If} $1 != 0<br />
             StrCpy $0 &quot;failed to iterate: $1&quot;<br />
         ${Else} <br />
             ${If} $6 &gt; 0<br />
                 ; Variant<br />
                 ; (unsigned short vt, WORD wReserved1, <br />
                 ;  WORD wReserved2, WORD wReserved3, BSTR bstrVal)<br />
                 ; Allocate memory for Variant<br />
                 System::Call &quot;*(i 0, i 0, i 0) i.r7&quot;<br />
                 ; Call IWbemClassObject-&gt;Get<br />
                 System::Call &quot;$5-&gt;4(w 'SerialNumber', i 0, i r7, i 0, i 0) i.r1&quot;<br />
                 ${If} $1 &lt; 0<br />
                    StrCpy $0 &quot;failed to get: $1&quot;<br />
                 ${Else} <br />
                    ; Access bstrVal from Variant<br />
                    System::Call &quot;*$7(i, i, w .r0)&quot;<br />
                    System::Call &quot;ole32::VariantClear(i r7)&quot;<br />
                 ${EndIf}<br />
                 ; Free memory associated with Variant<br />
                 System::Free $7<br />
             ${Else}<br />
                 StrCpy $0 &quot;failed: no items [$6]&quot;<br />
             ${EndIf}<br />
             ; Call IWbemClassObject-&gt;Release<br />
             System::Call &quot;$5-&gt;2()&quot;<br />
         ${EndIf}<br />
         ; Call IEnumWbemClassObject-&gt;Release<br />
         System::Call &quot;$4-&gt;2()&quot;<br />
     ${EndIf}<br />
<br />
     ; Call IWbemService-&gt;Release<br />
     System::Call &quot;$3-&gt;2()&quot;<br />
  ${EndIf}<br />
<br />
  ; Call IWbemLocator-&gt;Release<br />
  System::Call &quot;$2-&gt;2()&quot;<br />
<br />
bios_sn_end:<br />
  ; Restore registers<br />
  Pop $7<br />
  Pop $6<br />
  Pop $5<br />
  Pop $4<br />
  Pop $3<br />
  Pop $2<br />
  Pop $1<br />
FunctionEnd<br />
<br />
Function .onInit<br />
  InitPluginsDir<br />
  Call bios_sn<br />
  MessageBox MB_OK &quot;BIOS Serial Number=$0&quot;<br />
  Quit<br />
FunctionEnd<br />
<br />
Section <br />
SectionEnd</div></div><hr />


<div class="post"><div class="posttop"><div class="username">jpderuiter</div><div class="date">13th August 2009, 21:09</div></div><div class="posttext">Wow, this is a very good example of how to use the System plugin.<br />
<br />
Anyhow, you could have used the WMIInspector plugin to get the same resultWmiInspector::Request &quot;CIMV2&quot; &quot;Win32_BIOS&quot; &quot;SerialNumber&quot;<br />
Pop $0 <br />
Pop $1<br />
MessageBox MB_OK &quot;Result call: $0$\r$\n$\r$\nValue: $1&quot;</div></div><hr />


<div class="post"><div class="posttop"><div class="username">f0rt</div><div class="date">14th August 2009, 20:00</div></div><div class="posttext">The WMIInspector plugin is fine as well.<br />
<br />
The advantage of the System plugin is that it is shipped with NSIS.<br />
<br />
Somehow a backslash (\) got lost in the call to IWbemLocator-&gt;ConnectServer. It should be:<br />
<br />
<br />
  ; Call IWbemLocator-&gt;ConnectServer<br />
  System::Call &quot;$2-&gt;3(w 'ROOT\\CIMV2', i 0, i 0, i 0, i 0, i 0, i 0, *i .r3) i.r1&quot; <br />
<br />
<br />
I could no longer edit my previous post. So I just replied to the thread.</div></div><hr />


<div class="post"><div class="posttop"><div class="username">f0rt</div><div class="date">18th August 2009, 23:20</div></div><div class="posttext">The disadvantage of the System plugin is that you need to be very careful with allocating and freeing memory.<br />
<br />
So here comes a new version which frees the previously allocated memory for the variant.<br />
<br />
<br />
OutFile &quot;bios_sn.exe&quot;<br />
<br />
!include &quot;LogicLib.nsh&quot;<br />
<br />
!define CLSCTX_INPROC_SERVER 1<br />
!define CLSID_WbemLocator {4590f811-1d3a-11d0-891f-00aa004b2e24}<br />
!define IID_IWbemLocator {dc12a687-737f-11cf-884d-00aa004b2e24}<br />
<br />
!define RPC_C_AUTHN_LEVEL_DEFAULT   0<br />
!define RPC_C_IMP_LEVEL_IMPERSONATE 3<br />
<br />
!define EOAC_NONE                   0<br />
<br />
!define WBEM_FLAG_FORWARD_ONLY	     0x20<br />
!define WBEM_FLAG_RETURN_IMMEDIATELY 0x10<br />
!define WBEM_INFINITE	             0xffffffff<br />
<br />
!define VT_BSTR                      8<br />
<br />
Function bios_sn<br />
  ; Save registers<br />
  Push $1<br />
  Push $2<br />
  Push $3<br />
  Push $4<br />
  Push $5<br />
  Push $6<br />
  Push $7<br />
<br />
  ; Initialization of COM is done via OleInitialize in NSIS installer code<br />
  ; Set general COM security level<br />
  System::Call &quot;ole32::CoInitializeSecurity( \\<br />
        i 0, i -1, i 0, i 0, i ${RPC_C_AUTHN_LEVEL_DEFAULT}, \\<br />
        i ${RPC_C_IMP_LEVEL_IMPERSONATE}, i 0, i ${EOAC_NONE}, i 0) i.r1&quot;<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to initialize security: $1&quot;<br />
     Goto bios_sn_end<br />
  ${EndIf}<br />
  <br />
  ; Create IWbemLocator interface<br />
  System::Call &quot;ole32::CoCreateInstance( \\<br />
        g '${CLSID_WbemLocator}', i 0, \\<br />
        i ${CLSCTX_INPROC_SERVER}, \\<br />
        g '${IID_IWbemLocator}', *i .r2) i.r1&quot;<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to create IWebmLocator object: $1&quot;<br />
     Goto bios_sn_end<br />
  ${EndIf}<br />
<br />
  ; Call IWbemLocator-&gt;ConnectServer<br />
  System::Call &quot;$2-&gt;3(w 'ROOT\\CIMV2', i 0, i 0, i 0, i 0, i 0, i 0, *i .r3) i.r1&quot;<br />
<br />
  ${If} $1 != 0<br />
     StrCpy $0 &quot;failed to connect: $1&quot;<br />
  ${Else}<br />
     ; Call IWbemServices-&gt;ExecQuery <br />
     System::Call &quot;$3-&gt;20(w 'WQL', w 'Select SerialNumber from Win32_BIOS', \\<br />
        ${WBEM_FLAG_FORWARD_ONLY} | ${WBEM_FLAG_RETURN_IMMEDIATELY}, \\<br />
        i 0, *i .r4) i.r1&quot;<br />
     ${If} $1 != 0<br />
         StrCpy $0 &quot;failed to query: $1 $3&quot;<br />
     ${Else}<br />
         ; Call IEnumWbemClassObject-&gt;Next<br />
         System::Call &quot;$4-&gt;4(i ${WBEM_INFINITE}, i 1, *i .r5, *i .r6) i.r1&quot;<br />
         ${If} $6 != 0<br />
             ; Variant<br />
             ; (unsigned short vt, WORD wReserved1, WORD wReserved2, WORD wReserved3, BSTR bstrVal)<br />
             System::Call &quot;*(i 0, i 0, i 0) i.r7&quot;<br />
             ${If} $7 != 0<br />
                ; Call IWbemClassObject-&gt;Get<br />
                System::Call &quot;$5-&gt;4(w 'SerialNumber', i 0, i r7, i 0, i 0) i.r1&quot;<br />
                ${If} $1 != 0<br />
                   StrCpy $0 &quot;failed to get: $1&quot;<br />
                ${Else} <br />
                   ; Check type of variant<br />
                   System::Call &quot;*$7(i .r1)&quot;<br />
                   IntOp $1 $1 &amp; 0xFFFF<br />
                   ${If} $1 == ${VT_BSTR}<br />
                      ; Access bstrVal from Variant<br />
                      System::Call &quot;*$7(i, i, w .r0)&quot;<br />
                   ${Else}<br />
                      StrCpy $0 &quot;failed to access data: wrong type [$1]&quot;<br />
                   ${EndIf}<br />
                   System::Call &quot;ole32::VariantClear(i r7)&quot;<br />
                ${EndIf}<br />
                ; Deallocate memory of variant<br />
                System::Free $7<br />
             ${Else}<br />
                StrCpy $0 &quot;failed to allocate memory for variant&quot;<br />
             ${EndIf}<br />
             ; Call IWbemClassObject-&gt;Release<br />
             System::Call &quot;$5-&gt;2()&quot;<br />
         ${Else}<br />
             StrCpy $0 &quot;failed to iterate [$6]: $1&quot;<br />
         ${EndIf}<br />
         ; Call IEnumWbemClassObject-&gt;Release<br />
         System::Call &quot;$4-&gt;2()&quot;<br />
     ${EndIf}<br />
<br />
     ; Call IWbemService-&gt;Release<br />
     System::Call &quot;$3-&gt;2()&quot;<br />
  ${EndIf}<br />
<br />
  ; Call IWbemLocator-&gt;Release<br />
  System::Call &quot;$2-&gt;2()&quot;<br />
<br />
bios_sn_end:<br />
  ; Restore registers<br />
  Pop $7<br />
  Pop $6<br />
  Pop $5<br />
  Pop $4<br />
  Pop $3<br />
  Pop $2<br />
  Pop $1<br />
FunctionEnd<br />
<br />
Function .onInit<br />
  InitPluginsDir<br />
  Call bios_sn<br />
  MessageBox MB_OK &quot;Bios Serial Number=$0&quot;<br />
  Quit<br />
FunctionEnd<br />
<br />
Section <br />
SectionEnd</div></div><hr />


<div id="copyright">2001-2013 <a href="http://creativecommons.org/publicdomain/mark/1.0/" target="_blank">Public Domain Mark 1.0</a></div>
</div>
</body>
</html>