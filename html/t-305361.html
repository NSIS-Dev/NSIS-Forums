<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en">
<head>
  <meta charset="utf-8" />
  <meta http-equiv="X-UA-Compatible" content="IE=edge" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0" />
  <meta name="description" content="Start Menu Folder displays &quot;0&quot; in UAC plug in sample" />

  <title>Start Menu Folder displays "0" in UAC plug in sample - NSIS Forums</title>
  <script>(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)})(window,document,'script','https://www.google-analytics.com/analytics.js','ga');ga('create','UA-53539506-8','auto');ga('send','pageview');</script>
  <!-- Bootstrap core CSS -->

  <link rel="stylesheet" type="text/css" href="//netdna.bootstrapcdn.com/bootswatch/3.0.1/readable/bootstrap.min.css" />
  <link rel="stylesheet" href="../css/style.css" type="text/css" /><!-- HTML5 shim and Respond.js IE8 support of HTML5 elements and media queries -->
  <!--[if lt IE 9]>
                <script src="https://oss.maxcdn.com/libs/html5shiv/3.7.0/html5shiv.js"></script>
                <script src="https://oss.maxcdn.com/libs/respond.js/1.3.0/respond.min.js"></script>
        <![endif]-->
</head>

<body>
  <div class="container">
    <!-- Put navigation bar here -->

    <ol class="breadcrumb">
      <li><a href="../index.html" class="glyphicon glyphicon-home"></a></li>

      <li><a href="f-65.html">NSIS Discussion</a></li>

      <li class="active">Start Menu Folder displays "0" in UAC plug in sample</li>
    </ol>

    <p class="lead text-muted">Archive: <a title="Try the Internet Archive" target="_blank" href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=305361">Start Menu Folder displays "0" in UAC plug in sample</a></p><br />

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">bedbuffer</span><br />
      <span class="post-time small text-muted">15th April 2009 07:41 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content"><strong>Start Menu Folder displays "0" in UAC plug in sample</strong><br />
      Hi All,<br />
      <br />
      I downloaded the UAC plugin from NSIS' site and along with the plug in are some samples for it, I came across "UAC_GetUserShellFolderPath.nsi." It works great but there seems to be a small problem with it.<br />
      <br />
      When you get to the installation page that says "Choose Start Menu Folder," the destination folder for the start menu by default is "0" when you select "user" for install type, and "1" when you select "admin" for install type. It's suppose to display the name of your application by default, not a number.<br />
      <br />
      Please help...<br />
      <br />
      Here's the script of the sample.<br />
      <br />
      /*<br />
      This sample supports two modes, installing as a normal user (single user install) AND as admin (all users install)<br />
      This sample uses the registry plugin, so you need to download that if you don't already have it<br />
      */<br />
      <br />
      !define APPNAME "UAC_RealWorldFullyLoadedDualMode"<br />
      !define ELEVATIONTITLE "${APPNAME}: Elevate" ;displayed during elevation on our custom page<br />
      !define SMSUBDIR "${APPNAME}"<br />
      !define UNINSTALLER_NAME "Uninstall ${APPNAME}.exe"<br />
      !define UNINSTALLER_REGSECTION "${APPNAME}"<br />
      !define RegPath.MSUninstall "Software\Microsoft\Windows\CurrentVersion\Uninstall"<br />
      Name "${APPNAME}"<br />
      OutFile "${APPNAME}.exe"<br />
      ShowInstDetails show<br />
      SetCompressor LZMA<br />
      RequestExecutionLevel user /* RequestExecutionLevel REQUIRED! */<br />
      !include MUI.nsh<br />
      !include UAC.nsh<br />
      !include LogicLib.nsh<br />
      !include Registry.nsh<br />
      !include nsDialogs.nsh ;for our custom page<br />
      !include FileFunc.nsh ;we need to parse the command line<br />
      <br />
      !insertmacro GetParameters<br />
      !insertmacro GetOptions<br />
      <br />
      !define MUI_CUSTOMFUNCTION_ABORT onAbort<br />
      !define MUI_CUSTOMFUNCTION_GUIINIT onGuiInit<br />
      !define MUI_COMPONENTSPAGE_NODESC<br />
      !define MUI_FINISHPAGE_NOAUTOCLOSE<br />
      !define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\llama-blue.ico"<br />
      !define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\llama-blue.ico"<br />
      !define MUI_WELCOMEPAGE_TITLE_3LINES<br />
      <br />
      var InstMode # 0: Single user, 1:All users, &gt;1:elevated instance, perform page jump<br />
      var hKey # Reg hive<br />
      var hSelModeAdminRadio<br />
      <br />
      /*!define MUI_CUSTOMFUNCTION_ABORT onUserAbort<br />
      Function onUserAbort<br />
      messagebox mb_ok onUserAbort<br />
      FunctionEnd<br />
      */<br />
      <br />
      !macro SetMode IsAdmin<br />
      !if "${IsAdmin}" &gt; 0<br />
      SetShellVarContext all<br />
      StrCpy $InstMode 1<br />
      StrCpy $hKey HKLM<br />
      !else<br />
      SetShellVarContext current<br />
      StrCpy $InstMode 0<br />
      StrCpy $hKey HKCU<br />
      !endif<br />
      !macroend<br />
      <br />
      Function .OnInit<br />
      !insertmacro SetMode 0<br />
      ${GetParameters} $R9<br />
      ${GetOptions} "$R9" UAC $0 ;look for special /UAC:???? parameter (sort of undocumented)<br />
      ${Unless} ${Errors}<br />
      UAC::IsAdmin<br />
      ${If} $0 &lt; 1<br />
      SetErrorLevel 666 ;special return value for outer instance so it knows we did not have admin rights<br />
      Quit<br />
      ${EndIf}<br />
      !insertmacro SetMode 1<br />
      StrCpy $InstMode 2<br />
      ${EndIf}<br />
      FunctionEnd<br />
      <br />
      Function onGuiInit<br />
      ${If} $InstMode &gt;= 2<br />
      ${UAC.GetOuterInstanceHwndParent} $0<br />
      ${If} $0 &lt;&gt; 0<br />
      System::Call /NOUNLOAD "*(i,i,i,i)i.r1"<br />
      System::Call /NOUNLOAD 'user32::GetWindowRect(i $0,i r1)i.r2'<br />
      ${If} $2 &lt;&gt; 0<br />
      System::Call /NOUNLOAD "*$1(i.r2,i.r3)"<br />
      System::Call /NOUNLOAD 'user32::SetWindowPos(i $hwndParent,i0,ir2,ir3,i0,i0,i 4|1)'<br />
      ${EndIf}<br />
      ShowWindow $hwndParent ${SW_SHOW}<br />
      ShowWindow $0 ${SW_HIDE} ;hide outer instance installer window<br />
      System::Free $1<br />
      ${EndIf}<br />
      ${EndIf}<br />
      FunctionEnd<br />
      <br />
      Function Un.OnInit<br />
      !insertmacro SetMode 0<br />
      ReadRegDWORD $0 HKLM "${RegPath.MSUninstall}\${UNINSTALLER_REGSECTION}" InstMode ;We saved the "mode" in the installer<br />
      ${If} $0 U&gt; 0<br />
      ; If it was installed for all users, we have to be admin to uninstall it<br />
      ${UAC.U.Elevate.AdminOnly} "${UNINSTALLER_NAME}"<br />
      !insertmacro SetMode 1<br />
      ${EndIf}<br />
      FunctionEnd<br />
      <br />
      Function onAbort<br />
      ${UAC.Unload}<br />
      FunctionEnd<br />
      <br />
      ${UAC.AutoCodeUnload} 1 ;Auto-generate .OnInstFailed and .OnInstSuccess functions<br />
      <br />
      !define MUI_PAGE_CUSTOMFUNCTION_PRE SkipPageInElvModePreCB<br />
      !insertmacro MUI_PAGE_WELCOME<br />
      Page Custom ModeSelectionPageCreate ModeSelectionPageLeave<br />
      !define MUI_PAGE_CUSTOMFUNCTION_PRE CmpntsPreCB<br />
      !insertmacro MUI_PAGE_COMPONENTS<br />
      !define MUI_PAGE_CUSTOMFUNCTION_PRE DirPreCB<br />
      !insertmacro MUI_PAGE_DIRECTORY<br />
      !insertmacro MUI_PAGE_STARTMENU 1 $9<br />
      !insertmacro MUI_PAGE_INSTFILES<br />
      !define MUI_FINISHPAGE_TITLE_3LINES<br />
      !define MUI_FINISHPAGE_RUN<br />
      !define MUI_FINISHPAGE_RUN_FUNCTION FinishRunCB<br />
      !insertmacro MUI_PAGE_FINISH<br />
      !define MUI_WELCOMEPAGE_TITLE_3LINES<br />
      !insertmacro MUI_UNPAGE_WELCOME<br />
      !insertmacro MUI_UNPAGE_INSTFILES<br />
      !insertmacro MUI_LANGUAGE "English"<br />
      <br />
      Function CmpntsPreCB<br />
      GetDlgItem $0 $hwndparent 3<br />
      ${IfThen} $InstMode &gt;= 1 ${|} EnableWindow $0 0 ${|} ;prevent user from going back and selecting single user so noobs don't end up installing as the wrong user<br />
      FunctionEnd<br />
      <br />
      Function SkipPageInElvModePreCB<br />
      ${IfThen} $InstMode &gt; 1 ${|} Abort ${|} ;skip this page so we get to the mode selection page<br />
      FunctionEnd<br />
      <br />
      Function ModeSelectionPageCreate<br />
      ${If} $InstMode &gt; 1<br />
      StrCpy $InstMode 1<br />
      Abort ;skip this page and contine where the "parent" would have gone<br />
      ${EndIf}<br />
      !insertmacro MUI_HEADER_TEXT_PAGE "Select install type" "Blah blah blah blah"<br />
      nsDialogs::Create /NOUNLOAD 1018<br />
      Pop $0<br />
      ${NSD_CreateLabel} 0 20u 75% 20u "Blah blah blah blah select install type..."<br />
      Pop $0<br />
      System::Call "advapi32::GetUserName(t.r0, *i ${NSIS_MAX_STRLEN}r1) i.r2"<br />
      ${NSD_CreateRadioButton} 0 40u 75% 15u "Single User ($0)"<br />
      Pop $0<br />
      ${IfThen} $InstMode U&lt; 1 ${|} SendMessage $0 ${BM_SETCHECK} 1 0 ${|}<br />
      ${NSD_CreateRadioButton} 0 60u 75% 15u "All users"<br />
      Pop $hSelModeAdminRadio<br />
      ${IfThen} $InstMode U&gt; 0 ${|} SendMessage $hSelModeAdminRadio ${BM_SETCHECK} 1 0 ${|}<br />
      nsDialogs::Show<br />
      FunctionEnd<br />
      <br />
      !macro EnableCtrl dlg id state<br />
      push $0<br />
      GetDlgItem $0 ${dlg} ${id}<br />
      EnableWindow $0 ${state}<br />
      pop $0<br />
      !macroend<br />
      <br />
      Function ModeSelectionPageLeave<br />
      SendMessage $hSelModeAdminRadio ${BM_GETCHECK} 0 0 $9<br />
      UAC::IsAdmin<br />
      ${If} $9 U&gt; 0<br />
      ${AndIf} $0 U&lt; 1<br />
      System::Call /NoUnload 'user32::GetWindowText(i $HwndParent,t.R1,i ${NSIS_MAX_STRLEN})' ;get original window title<br />
      System::Call /NoUnload 'user32::SetWindowText(i $HwndParent,t "${ELEVATIONTITLE}")' ;set out special title<br />
      StrCpy $2 "" ;reset special return, only gets set when sub process is executed, not when user cancels<br />
      !insertmacro EnableCtrl $HWNDParent 1 0 ;Disable next button, just because it looks good ;)<br />
      ${UAC.RunElevatedAndProcessMessages}<br />
      !insertmacro EnableCtrl $HWNDParent 1 1<br />
      System::Call 'user32::SetWindowText(i $HwndParent,t "$R1")' ;restore title<br />
      ${If} $2 = 666 ;our special return, the new process was not admin after all<br />
      MessageBox mb_iconExclamation "You need to login with an account that is a member of the admin group to continue..."<br />
      Abort<br />
      ${ElseIf} $0 = 1223 ;cancel<br />
      Abort<br />
      ${Else}<br />
      ${If} $0 &lt;&gt; 0<br />
      ${If} $0 = 1062<br />
      MessageBox mb_iconstop "Unable to elevate, Secondary Logon service not running!"<br />
      ${Else}<br />
      MessageBox mb_iconstop "Unable to elevate, error $0 ($1,$2,$3)"<br />
      ${EndIf}<br />
      Abort<br />
      ${EndIf}<br />
      ${EndIf}<br />
      Quit ;We now have a new process, the install will continue there, we have nothing left to do here<br />
      ${EndIf}<br />
      FunctionEnd<br />
      <br />
      Function DirPreCB<br />
      ${If} $InstDir == ""<br />
      ${If} $InstMode U&gt; 0<br />
      StrCpy $InstDir "$PROGRAMFILES\${APPNAME}"<br />
      ${Else}<br />
      StrCpy $InstDir "$APPDATA\${APPNAME}"<br />
      ${EndIf}<br />
      ${EndIf}<br />
      FunctionEnd<br />
      <br />
      Function FinishRunCB<br />
      UAC::Exec "" "Notepad.exe" "$Windir\Win.INI" "$InstDir"<br />
      FunctionEnd<br />
      <br />
      Function CreateSMShortcuts<br />
      CreateDirectory "$SMPrograms\${SMSUBDIR}"<br />
      CreateShortcut "$SMPrograms\${SMSUBDIR}\${APPNAME}.lnk" "$Windir\Notepad.exe"<br />
      CreateShortcut "$SMPrograms\${SMSUBDIR}\Uninstall ${APPNAME}.lnk" "$InstDir\${UNINSTALLER_NAME}"<br />
      FunctionEnd<br />
      Function CreateDeskShortcuts<br />
      CreateShortcut "$Desktop\${APPNAME}.lnk" "$Windir\Notepad.exe"<br />
      FunctionEnd<br />
      <br />
      Section "!Required files"<br />
      SectionIn RO<br />
      SetOutPath -<br />
      !insertmacro _UAC.DbgDetailPrint ;some debug info, useful during testing<br />
      ;Install files here...<br />
      WriteUninstaller "$InstDir\${UNINSTALLER_NAME}"<br />
      ${registry::Write} "$hKey\${RegPath.MSUninstall}\${UNINSTALLER_REGSECTION}" DisplayName "${APPNAME}" REG_SZ $0<br />
      ${registry::Write} "$hKey\${RegPath.MSUninstall}\${UNINSTALLER_REGSECTION}" UninstallString "$InstDir\${UNINSTALLER_NAME}" REG_SZ $0<br />
      ${registry::Write} "$hKey\${RegPath.MSUninstall}\${UNINSTALLER_REGSECTION}" InstMode $InstMode REG_DWORD $0<br />
      ${registry::Unload}<br />
      SectionEnd<br />
      <br />
      ; CREATING SHORTCUTS FOR THE INTERACTIVE USER IN A "ALL USERS" TYPE INSTALL IS BAD PRACTICE,<br />
      ; BUT WE DO IT HERE FOR DEMONSTRATION PURPOSES<br />
      Section "Startmenu Shortcuts"<br />
      ${UAC.CallFunctionAsUser} CreateSMShortcuts<br />
      SectionEnd<br />
      Section "Desktop Shortcut"<br />
      ${UAC.CallFunctionAsUser} CreateDeskShortcuts<br />
      SectionEnd<br />
      <br />
      Section Uninstall<br />
      Delete "$InstDir\${UNINSTALLER_NAME}"<br />
      Delete "$SMPrograms\${SMSUBDIR}\${APPNAME}.lnk"<br />
      Delete "$SMPrograms\${SMSUBDIR}\Uninstall ${APPNAME}.lnk"<br />
      RMDir "$SMPrograms\${SMSUBDIR}"<br />
      Delete "$Desktop\${APPNAME}.lnk"<br />
      <br />
      RMDir "$InstDir"<br />
      ${registry::DeleteKey} "$hKey\${RegPath.MSUninstall}\${UNINSTALLER_REGSECTION}" $0<br />
      ${registry::Unload}<br />
      SectionEnd</p>
    </div>
    <hr />

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Anders</span><br />
      <span class="post-time small text-muted">15th April 2009 09:46 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">you should not use $9 as the startmenu variable, make a new var and define SMSUBDIR as that var (That code is just a example, and the startmenu folder name was hardcoded, it will be fixed in the next version)</p>
    </div>
    <hr />

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">bedbuffer</span><br />
      <span class="post-time small text-muted">16th April 2009 03:26 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">My other question is that !insertmacro MUI_STARTMENU_GETFOLDER 1 $9 uses "1" before the variable $9. Usually what I see is !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder. What's the difference? thanks...</p>
    </div>
    <hr />

    <div class="footer">
      <p class="text-muted small">Fork me on <a href="https://github.com/nsis-dev/NSIS-Forums/">GitHub</a></p>
    </div>
  </div><script src="../js/highlight.pack.js" type="text/javascript">
</script><script type="text/javascript">
//<![CDATA[
  hljs.initHighlightingOnLoad();
  //]]>
  </script>
</body>
</html>
