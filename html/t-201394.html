<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en"><head><meta charset="utf-8"><meta http-equiv="X-UA-Compatible" content="IE=edge"><meta name="viewport" content="width=device-width,initial-scale=1"><meta name="description" content="Recursive File/Directory Operations"><title>Recursive File/Directory Operations - NSIS Forums</title><script>(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)})(window,document,'script','https://www.google-analytics.com/analytics.js','ga');ga('create','UA-53539506-8','auto');ga('send','pageview');</script><link rel="preload" href="https://cdnjs.cloudflare.com/ajax/libs/bootswatch/3.3.7/readable/bootstrap.min.css" as="style" onload="this.rel='stylesheet'"><link rel="preload" href="../css/style.css" as="style" onload="this.rel='stylesheet'"><noscript><link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/bootswatch/3.3.7/readable/bootstrap.min.css"><link rel="stylesheet" href="../css/style.css"></noscript><script>!function(a){"use strict";var b=function(b,c,d){function e(a){return h.body?a():void setTimeout(function(){e(a)})}function f(){i.addEventListener&&i.removeEventListener("load",f),i.media=d||"all"}var g,h=a.document,i=h.createElement("link");if(c)g=c;else{var j=(h.body||h.getElementsByTagName("head")[0]).childNodes;g=j[j.length-1]}var k=h.styleSheets;i.rel="stylesheet",i.href=b,i.media="only x",e(function(){g.parentNode.insertBefore(i,c?g:g.nextSibling)});var l=function(a){for(var b=i.href,c=k.length;c--;)if(k[c].href===b)return a();setTimeout(function(){l(a)})};return i.addEventListener&&i.addEventListener("load",f),i.onloadcssdefined=l,l(f),i};"undefined"!=typeof exports?exports.loadCSS=b:a.loadCSS=b}("undefined"!=typeof global?global:this);!function(a){if(a.loadCSS){var b=loadCSS.relpreload={};if(b.support=function(){try{return a.document.createElement("link").relList.supports("preload")}catch(b){return!1}},b.poly=function(){for(var b=a.document.getElementsByTagName("link"),c=0;c<b.length;c++){var d=b[c];"preload"===d.rel&&"style"===d.getAttribute("as")&&(a.loadCSS(d.href,d,d.getAttribute("media")),d.rel=null)}},!b.support()){b.poly();var c=a.setInterval(b.poly,300);a.addEventListener&&a.addEventListener("load",function(){b.poly(),a.clearInterval(c)}),a.attachEvent&&a.attachEvent("onload",function(){a.clearInterval(c)})}}}(this);</script></head><body><div class="container"><ol class="breadcrumb"><li><a href="../index.html" class="glyphicon glyphicon-home"></a></li><li><a href="f-65.html">NSIS Discussion</a></li><li class="active">Recursive File/Directory Operations</li></ol><p class="lead text-muted">Archive: <a title="Try the Internet Archive" target="_blank" href="http://web.archive.org/web/*/forums.winamp.com/showthread.php?t=201394">Recursive File/Directory Operations</a></p><br><div class="post"><p class="post-meta"><span class="post-author text-primary">RobGrant</span><br><span class="post-time small text-muted">7th December 2004 12:34 <abbr title="Coordinated Universal Time">UTC</abbr></span></p><p class="post-content"><strong>Recursive File/Directory Operations</strong><br>Guys. I've been looking at this stuff for a while now, and getting all tangled up in knots, hope someone can help me through this one a bit :)<br><br>I need to move the contents of a directory (within which there are directories and files, each of which can contain directories and/or files, etc). I need to not move it en masse, but one at a time, as I need to perform an extra operation on each file that I move.<br><br>I need to retain the folder structure as well. Currently I have this, which a) doesn't work properly, and b) doesn't retain the folder structure:<br><br></p><pre>
<code><br>Push $1<br>Push $8<br>Push $9<br>StrCpy $9 "$INSTDIR\scripts-temp\"<br>  <br>startSearch:<br>  FindFirst $0 $1 "$9\*"<br>  StrCpy $8 "0"<br>  loop:<br>    StrCmp $1 "" dirdone<br>    IfFileExists "$9\$1\*.*" IsDir NotDir<br>    IsDir:<br>      StrCmp $1 "." bleh<br>      StrCmp $1 ".." bleh<br>      Push "$9$1\"<br>      IntOp $8 $8 + "1"<br>      Goto bleh<br>    NotDir:<br>      !insertmacro WRITE_TO_MAIN_LOG "Copying file $9$1 to $INSTDIR\scripts\" "**"<br>      <br>      !insertmacro COPY_FILE $9$1 "$INSTDIR\scripts\"<br>    bleh:<br>    FindNext $0 $1<br>    Goto loop<br>  dirdone:<br>    IntCmp $8 "0" done<br>    IntOp $8 $8 - 1<br>    Pop $9<br>    Goto startSearch<br><br>done:<br>Pop $9<br>Pop $8<br>Pop $1<br></code>
</pre><br>
      <br>
      For completeness, here is the COPY_FILE macro:<br>
      <br>
      <pre>
<code><br>!macro COPY_FILE OLD_FILEPATH NEW_FILEPATH<br>  ClearErrors<br>  Rename "${OLD_FILEPATH}" "${NEW_FILEPATH}"<br>  IfErrors +4<br>    !insertmacro WRITE_TO_MAIN_LOG "Wrote file ${NEW_FILEPATH}" "**"<br>    !insertmacro WRITE_TO_FILELIST_LOG "${NEW_FILEPATH}"<br>  Goto +2<br>    !insertmacro WRITE_TO_MAIN_LOG "Unable to copy file '${OLD_FILEPATH}' to '${NEW_FILEPATH}'" "EE"<br>!macroend<br></code>
</pre><br>
      <br>
      Thanks guys, sorry to lumber you with a nasty one like this :)<br>
      <br>
      -rob-
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Afrow UK</span><br>
      <span class="post-time small text-muted">7th December 2004 13:49 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">I have a function which I've been using over many installers in the past which you can use. You'll have to wait till I get home though because the function isn't on the Archive.<br>
      <br>
      -Stu</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">7th December 2004 13:56 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Brilliant :) Thanks.</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">7th December 2004 14:08 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">I'm getting there, just a quick bugfix and it copies it properly, but again without recreating the directory structure:<br>
      <br></p>
      <pre>
<code><br>Push $1<br>Push $8<br>Push $9<br>StrCpy $9 "$INSTDIR\scripts-temp\"<br>  <br>StrCpy $8 "0"<br>startSearch:<br>  FindFirst $0 $1 "$9\*"<br>  loop:<br>    StrCmp $1 "" dirdone<br>    IfFileExists "$9\$1\*.*" IsDir NotDir<br>    IsDir:<br>      StrCmp $1 "." bleh<br>      StrCmp $1 ".." bleh<br>      Push "$9$1\"<br>      IntOp $8 $8 + 1<br>      Goto bleh<br>    NotDir:<br>      !insertmacro COPY_FILE $9$1 "$INSTDIR\scripts\$1"<br>    bleh:<br>      FindNext $0 $1<br>      Goto loop<br>  dirdone:<br>    IntCmp $8 "0" done<br>    IntOp $8 $8 - 1<br>    Pop $9<br>    Goto startSearch<br><br>done:<br>Pop $9<br>Pop $8<br>Pop $1<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Afrow UK</span><br>
      <span class="post-time small text-muted">7th December 2004 17:28 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content"></p>
      <pre>
<code>StrCpy $DirPath "C:\blah"<br>StrCpy $Location "D:\blah"<br> Function GetFiles<br>  Push $R1<br>  Push $R2<br>  Push $R3<br>  Push $R4<br><br>  StrCpy $R3 1<br><br>  Push ""<br><br>  nextDir:<br>    Pop $R4<br>    IntOp $R3 $R3 - 1<br>    ClearErrors<br><br>    FindFirst $R1 $R2 "$DirPath$R4\*.*"<br>    checkLoop:<br>       IfFileExists "$DirPath$R4\$R2\*.*" +3<br><br>        ### Create directory<br>        CreateDirectory "$Location$R4"<br><br>       Goto checkDone<br>       FindNext $R1 $R2<br>       IfErrors 0 checkLoop<br>    checkDone:<br>    FindClose $R1<br><br>    FindFirst $R1 $R2 "$DirPath$R4\*.*"<br><br>    nextFile:<br>      StrCmp $R2 "." gotoNextFile<br>      StrCmp $R2 ".." gotoNextFile<br><br>      IfFileExists "$DirPath$R4\$R2\*.*" 0 notDir<br>          IntOp $R3 $R3 + 1<br>          Push "$R4\$R2"<br>          Goto gotoNextFile<br><br>      notDir:<br><br>          ### Copy a file<br>          CopyFiles "$DirPath$R4\$R2" "$Location$R2"<br><br>  gotoNextFile:<br>    FindNext $R1 $R2<br>    IfErrors 0 nextFile<br><br>    FindClose $R1<br><br>    StrCmp $R3 0 0 nextDir<br><br>  Pop $R4<br>  Pop $R3<br>  Pop $R2<br>  Pop $R1<br> FunctionEnd</code>
</pre><br>
      <br>
      You just need to adapt it now.<br>
      <br>
      -Stu
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Instructor</span><br>
      <span class="post-time small text-muted">8th December 2004 08:52 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Function Locate v1.2<br>
      <a href="http://nsis.sourceforge.net/archive/nsisweb.php?page=634&amp;instances=0" target="_blank">http://nsis.sourceforge.net/archive/...34&amp;instances=0</a><br>
      <br></p>
      <pre>
<code><br>Function Locate<br>;.....<br>FunctionEnd<br><br>Section<br>        StrCpy $R0 "C:\CM"   ;Directory copy from<br>        StrCpy $R1 "C:\CM2"  ;Directory copy into<br>        StrLen $R2 $R1<br><br>        !insertmacro Locate "$R0" "/L=F" "CallBack"<br><br>        IfErrors 0 +2<br>        MessageBox MB_OK 'error'<br>SectionEnd<br><br>Function CallBack<br>        StrCpy $1 $R8 '' $R2<br>        IfFileExists '$R1\$1\*.*' +2<br>        CreateDirectory '$R1\$1'<br>        CopyFiles /SILENT $R9 '$R1\$1'<br><br>        ;Your code ...<br>        ;$R9       "path\name" (source)<br>        ;$R8       "path"      (source)<br>        ;$R1\$1    "path"      (destination)<br>        ;$R7       "name"<br><br>        Push $0<br>FunctionEnd<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">8th December 2004 10:02 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Instructor, Afrow, cheers guys much appreciated.<br>
      <br>
      One question - in the Locate Callback, I want to be able to just copy the directories first, so I can log the file copying afterwards. If I comment out this line:<br>
      <br></p>
      <pre>
<code><br>CopyFiles /SILENT $R9 '$R1\$1'<br></code>
</pre><br>
      <br>
      ...then it doesn't make all of the directory structure, but it DOES make some of it!<br>
      <br>
      Any reason? CopyFiles docs don't seem to really say whether or not the function will create directories as well as files...<br>
      <br>
      Cheers<br>
      <br>
      -rob-
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Instructor</span><br>
      <span class="post-time small text-muted">8th December 2004 10:35 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content"></p>
      <pre>
<code><br>Function Locate<br>;.....<br>FunctionEnd<br><br>Section<br>        StrCpy $R0 "C:\CM"   ;Directory copy from<br>        StrCpy $R1 "C:\CM2"  ;Directory copy into<br>        StrLen $R2 $R1<br><br>        GetTempFileName $0<br>        FileOpen $R3 $0 w<br>        !insertmacro Locate "$R0" "/L=F" "CallBack"<br>        FileClose $R4<br><br>        IfErrors 0 +2<br>        MessageBox MB_OK 'error'<br><br>        Exec '"notepad.exe" "$0"'     ;view log<br>SectionEnd<br><br>Function CallBack<br>        StrCpy $1 $R8 '' $R2<br>        StrCmp $1 '' +2<br>        StrCpy $1 '\$1'<br>        IfFileExists '$R1\$1\*.*' +2<br>        CreateDirectory '$R1$1'<br>        CopyFiles /SILENT $R9 '$R1$1'<br><br>        ;Your code ...<br>        ;$R9       "path\name" (source)<br>        ;$R8       "path"      (source)<br>        ;$R1$1     "path"      (destination)<br>        ;$R7       "name"<br><br>        IfFileExists '$R1$1\$R7' 0 +3<br>        FileWrite $R3 "-old:$R9  -new:$R1$1\$R7  -success$\r$\n"<br>        goto +2<br>        FileWrite $R3 "-old:$R9  -new:$R1$1\$R7  -failed$\r$\n"<br><br>        Push $0<br>FunctionEnd<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">8th December 2004 11:43 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Is that FileClose $R4 meant to be $R3? I'm not being funny but this isn't working properly either way so it could well be right...</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Afrow UK</span><br>
      <span class="post-time small text-muted">8th December 2004 13:13 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Yes it should be.<br>
      <br>
      -Stu</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">8th December 2004 14:12 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Instructor - I tried to get that to work but it was weird, the $0 variable seemed to be very temperamental.<br>
      <br>
      Not sure what the problem was, so I went back to the solution I posted.<br>
      <br>
      Thanks anyway both of you, much appreciated!<br>
      <br>
      -rob-<br>
      <br>
      P.S. if anyone has helpful comments for other people who need to do this feel free to post...</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Instructor</span><br>
      <span class="post-time small text-muted">9th December 2004 08:07 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Sorry, I had little time for answer yesterday and I have done the pair of absurd mistakes.<br>
      <br>
      Copy files with log:<br></p>
      <pre>
<code><br>Section<br>        StrCpy $R0 "C:\CM"   ;Directory copy from<br>        StrCpy $R1 "C:\CM2"  ;Directory copy into<br>        StrLen $R2 $R0<br><br>        GetTempFileName $0<br>        FileOpen $R3 $0 w<br>        !insertmacro Locate "$R0" "/L=FDE" "CallBack"<br>        FileClose $R3<br><br>        IfErrors 0 +2<br>        MessageBox MB_OK 'error'<br><br>        Exec '"notepad.exe" "$0"'     ;view log<br>SectionEnd<br><br>Function CallBack<br>        StrCpy $1 $R8 '' $R2<br><br>        StrCmp $R6 '' 0 +3<br>        CreateDirectory '$R1$1\$R7'<br>        goto end<br>        CreateDirectory '$R1$1'<br>        CopyFiles /SILENT $R9 '$R1$1'<br><br>        IfFileExists '$R1$1\$R7' 0 +3<br>        FileWrite $R3 "-old:$R9  -new:$R1$1\$R7  -success$\r$\n"<br>        goto +2<br>        FileWrite $R3 "-old:$R9  -new:$R1$1\$R7  -failed$\r$\n"<br><br>        end:<br>        Push $0<br>FunctionEnd<br></code>
</pre><br>
      <br>
      <br>
      If you want recreate directory structure:<br>
      <pre>
<code><br>Section<br>        StrCpy $R0 "C:\CM"     ;Directory structure from<br>        StrCpy $R1 "C:\CM2"    ;Directory structure into<br>        StrLen $R2 $R0<br><br>        !insertmacro Locate "$R0" "/L=D" "CallBack"<br><br>        IfErrors 0 +2<br>        MessageBox MB_OK 'error'<br>SectionEnd<br><br>Function CallBack<br>        StrCpy $1 $R9 '' $R2<br>        StrCmp $1 '' +2<br>        CreateDirectory '$R1$1'<br><br>        Push $0<br>FunctionEnd<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">9th December 2004 10:53 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Instructor - thanks so much that was exactly what I needed. Here's the code just for reference purposes (the 2 logging macros aren't necessary) - it assumes that the original directory is $INSTDIR\&lt;var&gt;-temp and the new one is $INSTDIR\&lt;var&gt;, and that you want to delete the temp directory afterwards. Just Push the &lt;var&gt; having created the zip file and the 2 directories and go for it!<br>
      <br></p>
      <pre>
<code><br>Function unzipFiles<br>  Exch $R9<br>  CreateDirectory "$INSTDIR\$R9"<br>  NsExec::Exec '"unzip.exe" -o $R9.zip'<br><br>  StrCpy $R0 "$INSTDIR\$R9-temp"     ;Directory structure from<br>  StrCpy $R1 "$INSTDIR\$R9"          ;Directory structure into<br>  StrLen $R2 $R0<br>  StrCpy $0 $FILE_LOG_TEMP_FILELIST<br>  FileOpen $R3 $0 a<br>  FileSeek $R3 "0" END<br>  ClearErrors<br>  !insertmacro Locate "$R0" "/L=D" "CallBack-Directory"<br><br>  IfErrors 0 noscriptsdirerrors<br>  !insertmacro WRITE_TO_MAIN_LOG "Error writing directories within $R9\ directory" "EE"<br>  Goto writeScriptsFiles<br><br>  noscriptsdirerrors:<br>  !insertmacro WRITE_TO_MAIN_LOG "Finished writing directories within $R9\ directory" "**"<br><br>  writeScriptsFiles:<br>  ClearErrors<br>  !insertmacro Locate "$R0" "/L=FDE" "CallBack"<br>  FileClose $R3<br><br>  IfErrors 0 uzEnd<br>  !insertmacro WRITE_TO_MAIN_LOG "Error writing files within $R9\ directory" "EE"<br><br>  uzEnd:<br>  SetOutPath "$INSTDIR"<br>  RMDir /r "$INSTDIR\$R9-temp"<br>  Exch $R9<br>FunctionEnd<br><br>Function CallBack<br>        StrCpy $1 $R8 '' $R2<br><br>        StrCmp $R6 '' 0 +3<br>        CreateDirectory '$R1$1\$R7'<br>        goto end<br>        CreateDirectory '$R1$1'<br>        CopyFiles /SILENT $R9 '$R1$1'<br><br>        IfFileExists '$R1$1\$R7' 0 c-errors<br>        FileWrite $R3 "$R1$1\$R7$\r$\n"<br>        !insertmacro WRITE_TO_MAIN_LOG "Wrote file $R1$1\$R7" "**"<br>        goto end<br><br>        c-errors:<br>        !insertmacro WRITE_TO_MAIN_LOG "Failed to write $R1$1\$R7" "EE"<br><br>        end:<br>        Push $0<br>FunctionEnd<br><br>Function CallBack-Directory<br>        StrCpy $1 $R9 '' $R2<br>        StrCmp $1 '' +2<br><br>        ClearErrors<br>        CreateDirectory '$R1$1'<br>        IfErrors c-dir-errors<br>        FileWrite $R3 '$R1$1$\r$\n'<br>        !insertmacro WRITE_TO_MAIN_LOG "Created directory $R1$1\" "**"<br>        Goto end<br><br>        c-dir-errors:<br>        !insertmacro WRITE_TO_MAIN_LOG "Failed to create directory $R1$1\" "EE"<br><br>        end:<br>        Push $0<br>FunctionEnd<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">RobGrant</span><br>
      <span class="post-time small text-muted">10th December 2004 12:32 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">While we're on the subject of Locate, I thought I'd have a go at listing all Windows users of a computer, by listing the names of the directories which are immediate children of the C:\Documents and Settings folder (it's a Win2k or above installer so that's fine, AFAIK).<br>
      <br>
      To do that I do this:<br>
      <br></p>
      <pre>
<code><br>Function <br>...<br>  Push "$PROFILE"<br>  Call GetParent<br>  Pop $R0<br>  !insertmacro Locate "$R0" "/L=D /G=0" "findusers"<br>...<br>FunctionEnd<br><br>Function "findusers"<br>  MessageBox MB_OK "$R7"<br>FunctionEnd<br></code>
</pre><br>
      <br>
      Rather than giving me a nice sequence of messageboxes teling me user names, it crashes after the first one (Administrator in my case). Any ideas why?
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Instructor</span><br>
      <span class="post-time small text-muted">11th December 2004 10:43 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content"></p>
      <pre>
<code><br>Function "findusers"<br>        MessageBox MB_OK "$R7"<br><br>        Push $var     ; If $var=StopLocate Then exit from function<br>FunctionEnd<br></code>
</pre>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">atsuk</span><br>
      <span class="post-time small text-muted">12th December 2004 16:08 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">Afrow UK's example seems to be quite good short and easy, but the problem is that it does not complitelly support wildcards. suppose i need to find *.txt instead of *.*<br>
      in this case it only copyes files from $Location, but not from its subdirectories. And in case you only have $Location\subdir\file.txt it does not copy anything.<br>
      <br>
      does anybody have some ideas how to make it work with wildcards also?<br>
      <br>
      one thing should be different:<br>
      original line:<br>
      ### Copy a file<br>
      CopyFiles "$DirPath$R4\$R2" "$Location$R2"<br>
      should be replaced with<br>
      CopyFiles "$DirPath$R4\$R2" "$Location$R4\$R2"<br>
      <br>
      ..but still very good script(y)</p>
    </div>
    <hr>

    <div class="post">
      <p class="post-meta"><span class="post-author text-primary">Yathosho</span><br>
      <span class="post-time small text-muted">12th December 2004 16:30 <abbr title="Coordinated Universal Time">UTC</abbr></span></p>

      <p class="post-content">great, was just about to ask about such a function :)</p>
    </div>
    <hr>

    <div class="footer">
      <p class="text-muted small">Fork me on <a href="https://github.com/nsis-dev/NSIS-Forums/">GitHub</a></p>
    </div>
  </div><script src="../js/highlight.pack.js" type="text/javascript">
</script><script type="text/javascript">
//<![CDATA[
  hljs.initHighlightingOnLoad();
  //]]></script></body></html>